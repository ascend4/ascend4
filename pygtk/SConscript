Import('env')

import platform

srcs = Split("""
	library.cpp compiler.cpp type.cpp module.cpp symchar.cpp
	instance.cpp instanceinterfacedata.cpp
	matrix.cpp method.cpp name.cpp
	reporter.cpp simulation.cpp set.cpp units.cpp dimensions.cpp extmethod.cpp
	variable.cpp registry.cpp relation.cpp 
	solver.cpp curve.cpp plot.cpp
	solverparameters.cpp solverparameter.cpp solverparameteriterator.cpp
	solverstatus.cpp solverreporter.cpp
	incidencematrix.cpp
	integrator.cpp
	integratorreporter.cpp
	annotation.cpp
""")

# Build a static library with all the sources

python_env = env.Copy()

if platform.system()=='Windows' and env.get('MSVS'):
	python_env.Append(CCFLAGS=['/EHsc']) # for exceptions (as suggested by a MSVC error msg, dunno if it's right or not -- JP)

swig_has_gccvisibility = False
min,maj,pat = env['SWIGVERSION']
if min==1 and maj==3 and pat>=29:
	swig_has_gccvisibility = True

if env.get('HAVE_GCC'):
	#python_env.Append(CPPFLAGS=['-O3'])
	if swig_has_gccvisibility and env.has_key('HAVE_GCCVISIBILITY'):
		python_env.Append(CCFLAGS=['-fvisibility=hidden']);

objs = []

python_env.AppendUnique(CPPPATH=['#base/generic']+env['PYTHON_CPPPATH'])

for s in srcs:
	objs += python_env.SharedObject(s)	

#----------------------------------------------
# SWIG wrapper

def get_new_swig_flags(env):
	min,maj,pat = env['SWIGVERSION']
	flags = []
	if min==1 and maj==3 and pat>=28:
		flags += ['-O']
	sep = ":"
	if platform.system=="Windows": sep=";"

	print "FLAGS=%s" % env.get('MFGRAPH_CPPPATH')
	if env.get('WITH_MFGRAPH') and env.get('MFGRAPH_CPPPATH'):
		flags += ['-I%s' % i for i in env.get('MFGRAPH_CPPPATH').split(sep)]
	return flags

swig_env = python_env.Copy()
if '-Wall' in swig_env.get('CCFLAGS'):
	swig_env['CCFLAGS'] = swig_env['CCFLAGS'].remove('-Wall')

swigobjs = []

for swigf in Split("""
	ascpy.i
"""):
	swigobj = swig_env.SharedObject(swigf
		, SWIGFLAGS=['-python','-c++'] + get_new_swig_flags(env)
	)
	swig_env.SideEffect(['ascpy.py','ascpy_wrap.h'],'ascpy$SWIGCXXFILESUFFIX')
	swig_env.Depends('ascpy$SWIGCXXFILESUFFIX',['ascpy.i','solver.i'])
	swig_env.Clean('ascpy_wrap$SWIGCXXFILESUFFIX',swigobj)
	swig_env.Clean('ascpy.py','ascpy$SWIGCXXFILESUFFIX')
	swig_env.Clean('ascpy_wrap.h','ascpy$SWIGCXXFILESUFFIX')

	swigobjs.append(swigobj)

swig_env['LIBS'] = ['ascend']+env['PYTHON_LIB']
swig_env['LIBPATH'] = ['#'] + env['PYTHON_LIBPATH']
swig_env.Append(LINKFLAGS=env['PYTHON_LINKFLAGS'])

if not env.get('MSVS'):
	swig_env.Append(LIBS = ['stdc++'])

if env.get('WITH_DMALLOC'):
	swig_env.Append(LIBS = ['dmalloc'])
	swig_env.AppendUnique(LIBPATH = [env.get('DMALLOC_LIBPATH')])

if env.get('WITH_MFGRAPH'):
	swig_env.Append(LIBS = ['mfgraph'])
	swig_env.AppendUnique(LIBPATH = [env.get('MFGRAPH_LIBPATH')])

swiglib = swig_env.SharedLibrary("ascpy",objs + swigobjs
	, SHLIBPREFIX = '_'
)

#---------------------------------------------
# CONFIG & runtime shell script for posix

configpy = env.SubstInFile(source='config.py.in')
configh = env.SubstInFile(source='config.h.in')

if platform.system() != "Windows" or env.has_key('IS_MINGW'):
	ascendcmd = env.SubstInFile(source='ascend.in')
	env.AddPostAction(ascendcmd, 'chmod 755 $TARGET')
	ascdevcmd = env.SubstInFile(source='ascdev.in')
	env.AddPostAction(ascdevcmd, 'chmod 755 $TARGET')

#---------------------------------------------
# LITTLE WEE TEST PROGRAM for debuggin the c++ wrapper
# currently out of order because of need for a separate builddir due to ASCXX_WITH_PYTHON flag taking different value
#
#libascxx = env.SharedLibrary('ascxx',objs
#	, LIBS = ['ascend'] + env['PYTHON_LIB']
#	, LIBPATH = ['.'] + ['#'] + env['PYTHON_LIBPATH']
#)
#
idatest = swig_env.Program('idatest',['idatest.cpp'] + objs)
#
#env.Alias('ascxx',ascxxtest)

#---------------------------------------------
# INSTALLATION

# python compile bytecode

import py_compile
def pyc_build(target, source, env):
	py_compile.compile(str(source[0]),str(target[0]),"",True)
	return None	
env.Append(BUILDERS = {'PYC' : Builder(action = pyc_build,suffix='.pyc',src_suffix='.py')})

if env.get('CAN_INSTALL'):
	env.InstallProgram(env['INSTALL_ROOT']+env['INSTALL_BIN'],ascendcmd)

	import glob
	pythonfiles = glob.glob("*.py")

	pythonpycs = []
	for f in pythonfiles:
		pythonpycs.append( env.PYC(f) )

	for f in pythonfiles + pythonpycs:
		env.InstallShared(env['INSTALL_ROOT']+env['INSTALL_ASCDATA']+"/",f)

	env.InstallShared(env['INSTALL_ROOT']+env['INSTALL_ASCDATA']+"/","config.py")

	gladefiles = glob.glob("glade/*")
	env.InstallShared(env['INSTALL_ROOT']+env['INSTALL_ASCDATA']+"/glade/",gladefiles)

	env.InstallShared(env['INSTALL_ROOT']+env['INSTALL_ASCDATA']+"/",swiglib)

if platform.system()=="Windows":
	env.Append(NSISDEFINES={'OUTFILE':"#dist/"+env['WIN_INSTALLER_NAME']})
	installer = env.Installer('create.nsi')
	Depends(installer,[swiglib,configpy,configh,"../models","../ascend-config"])
	env.Alias('installer',installer)

# vim: set syntax=python:

