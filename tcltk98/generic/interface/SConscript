Import('env')

srcs = Split("""
	AscBitmaps.c AscPrintTcl.c BrowLogRel_io.c BrowWhen_io.c
	BrowserMethod.c BrowserProc.c BrowserQuery.c BrowserRel_io.c
	Commands.c DebugProc.c DisplayProc.c
	EnvVarProc.c HelpProc.c Integrators.c
	LibraryProc.c Lsode.c MtxProc.c ProbeProc.c Qlfdid.c
	ScriptProc.c Sensitivity.c SimsProc.c SlvProc.c
	SolverProc.c UnitsProc.c UserData.c
	tkConsole.c old_utils.c
""")

if env['HAVE_LEX']:
	typelexsource = 'typelex.l'
else:
	typelexsource = 'typelex_no_flex.c'

typelex = env.SharedObject(typelexsource
	, LEXFLAGS = '-ll -Ptyp_'
)

libs = ['ascend']
libpath = ['#']

libs.append(env['TCL_LIB'])
libs.append(env['TK_LIB'])

if env.has_key('TCL_LIBPATH'):
	libpath.append(env['TCL_LIBPATH'])
if env.has_key('TK_LIBPATH'):
	libpath.append(env['TK_LIBPATH'])

cppdefines = []
if env['STATIC_TKTABLE']:
	libpath.append(env['TKTABLE_LIBPATH'])
	libs.append(env['TKTABLE_LIB'])
	cppdefines.append('STATIC_TKTABLE')

cpppath = ['../../../base/generic']
if env.has_key('TCL_CPPPATH'):
	cpppath.append(env['TCL_CPPPATH'])

if env.has_key('TK_CPPPATH'):
	cpppath.append(env['TK_CPPPATH'])

env.Program("ascend4",srcs+typelex+['Driver.c']
	, CPPDEFINES = cppdefines
	, LIBS = libs
	, LIBPATH = libpath
	, CPPPATH = cpppath
#	, RPATH = ["C:\\Tcl\\lib"]
)
