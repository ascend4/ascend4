Import('env')

if env.get('WITH_IPOPT'):
	solver_env = env.Clone()

	solver_env.AppendUnique(CPPPATH=[solver_env.get('IPOPT_CPPPATH')])

	solver_env.Append(
		CPPDEFINES=[
			'ASC_WITH_IPOPT'
		]
	)

	lib = solver_env.SharedLibrary("ipopt",["asc_ipopt.c"]
		,LIBS = ['ascend'] + solver_env.get('IPOPT_LIBS')
		,LIBPATH = ['#']
		,SHLIBSUFFIX = env['EXTLIB_SUFFIX']
		,SHLIBPREFIX = env['EXTLIB_PREFIX']
	)

	env.Depends(lib,env['libascend'])

	env['extfns'] += [lib]

	if env.get('CAN_INSTALL'):
		dir = Dir(env.subst("$INSTALL_ROOT$INSTALL_SOLVERS"))
		env.InstallShared(dir,lib)

# vim: set syntax=python:
