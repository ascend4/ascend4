Import('env')
import platform

if env.get('WITH_IPOPT'):
	solver_env = env.Clone()

	solver_env.AppendUnique(CPPPATH=[solver_env.get('IPOPT_CPPPATH')])

	solver_env.Append(
		CPPDEFINES=[
			'ASC_WITH_IPOPT'
		]
		,CPPPATH = [solver_env.get('IPOPT_CPPPATH')]
	)
	
	if platform.system()=="Windows":
		solver_env.Append(
			CFLAGS=["-static"]
		)
	
	ll = env.get('IPOPT_LIBPATH')
	ll1 = []
	for l in ll:
		if "/usr/lib/gcc" not in l:
			ll1.append(l)

	solver_env.Append(
		LIBS = ['ascend'] + solver_env.get('IPOPT_LIBS')
		,LIBPATH = ['#'] + ll1
	)

	lib = solver_env.SharedLibrary("ipopt",["asc_ipopt.c"]
		,SHLIBSUFFIX = env['EXTLIB_SUFFIX']
		,SHLIBPREFIX = env['EXTLIB_PREFIX']
	)

	env.Depends(lib,env['libascend'])

	env['extfns'] += [lib]

	if env.get('CAN_INSTALL'):
		dir = Dir(env.subst("$INSTALL_ROOT$INSTALL_SOLVERS"))
		env.InstallShared(dir,lib)

# vim: set syntax=python:
