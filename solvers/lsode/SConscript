Import('env')

if 'LSODE' in env.get('WITH_SOLVERS'):

	solver_env = env.Copy()

	srcs = ['lsode.f','asc_lsode.c']

	linpack_srcs = ['d1mach.c','dgbfa.f','dgbsl.f','dgefa.f','dgesl.f']

	for s in linpack_srcs:
		srcs.append( "linpack/%s" % s )

	if env.get('F2C_LIBPATH'):
		solver_env.AppendUnique(LIBPATH=[env.get('F2C_LIBPATH')])

	if solver_env.get('F2C_LIB'):
		solver_env.AppendUnique(LIBS=[env.get('F2C_LIB')])

	solver_env.AppendUnique(
		LIBS=['ascend']
		,LIBPATH=['#']
	)

	lib = solver_env.SharedLibrary('lsode',srcs + env['blasobjs']
		,SHLIBSUFFIX = env['EXTLIB_SUFFIX']
		,SHLIBPREFIX = env['EXTLIB_PREFIX']
	)

	env.Depends(lib,env['libascend'])

	env['extfns'] += [lib]
	
	#TODO install the file?
	
	# vim: set syntax=python:
