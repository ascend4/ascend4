Import('env')

if env.get('WITH_IDA'):

	solver_env = env.Clone()

	if env.get('WITH_MMIO'):
		solver_env.Append(CPPPATH=["#/mmio"])

	if env.get('SUNDIALS_CPPPATH'):
		solver_env.Append(CPPPATH=[env.get('SUNDIALS_CPPPATH')])

	if env.get('SUNDIALS_CPPPATH_EXTRA'):
		solver_env.AppendUnique(
			CPPPATH=env.get('SUNDIALS_CPPPATH_EXTRA')
		)

	solver_env.Append(
		CPPDEFINES=[
			('SUNDIALS_VERSION_MAJOR',"$SUNDIALS_VERSION_MAJOR")
			,('SUNDIALS_VERSION_MINOR',"$SUNDIALS_VERSION_MINOR")
			,'ASC_WITH_IDA'
		]
	)

	solver_env.Append(
		LIBPATH=[env.get('SUNDIALS_LIBPATH'),"#"]
		,LIBS=[env.get('SUNDIALS_LIBS'),"ascend"]
	)

	srcs = ['ida.c', 'idacalc.c', 'idalinear.c', 'idaio.c', 'idaprec.c', 'idaanalyse.c', 'idaboundary.c']
	lib = solver_env.SharedLibrary("ida",srcs
		,SHLIBSUFFIX = env['EXTLIB_SUFFIX']
		,SHLIBPREFIX = env['EXTLIB_PREFIX']
	)

	env.Depends(lib,env['libascend'])

	env['extfns'] += lib
	
	print "WITH_IDA=",env.get('WITH_IDA')	
	
	if env.get('CAN_INSTALL'):
		dir = Dir(env.subst("$INSTALL_ROOT$INSTALL_SOLVERS"))
		env.InstallShared(dir,lib)

# vim: set syntax=python:
