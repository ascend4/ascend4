Import('env')

if 'IDA' in env['WITH_SOLVERS']:

	solver_env = env.Copy()

	if env.get('WITH_MMIO'):
		solver_env.Append(CPPPATH=["#/mmio"])

	if env.get('SUNDIALS_CPPPATH'):
		solver_env.Append(CPPPATH=[env.get('SUNDIALS_CPPPATH')])

	if env.get('SUNDIALS_CPPPATH_EXTRA'):
		solver_env.AppendUnique(
			CPPPATH=env.get('SUNDIALS_CPPPATH_EXTRA')
		)

	solver_env.Append(
		CPPDEFINES=[
			('SUNDIALS_VERSION_MAJOR',"$SUNDIALS_VERSION_MAJOR")
			,('SUNDIALS_VERSION_MINOR',"$SUNDIALS_VERSION_MINOR")
			,'ASC_WITH_IDA'
		]
	)

	solver_env.Append(
		LIBPATH=[env.get('SUNDIALS_LIBPATH')]
		, LIBS=[env.get('SUNDIALS_LIBS'),"ascend"]
	)

	srcs = ['ida.c','idalinear.c', 'idaanalyse.c']
	libida = solver_env.SharedLibrary("ida",srcs)

	env.Depends(libida,env['libascend'])

	env['extfns'] += libida

	if env.get('CAN_INSTALL'):
		dir = Dir(env.subst("$INSTALL_ROOT$INSTALL_SOLVERS"))
		env.InstallShared(dir,libida)

# vim: set syntax=python:
