(*
	This is the Arenstorf Orbit problem distributed as a sample problem with 
	the DOPRI5 integrator code.

	We're not really concerned with the details of this problem. We're just	
	trying to determine that our solver gets the right answer.	FORTRAN and
	C codes are provided that solve this problem; we aim to reproduce those
	results here using pure ASCEND code.

	Expected output from the FORTRAN version:

	 X =  0.00    Y =  0.9940000000E+00  0.0000000000E+00    NSTEP =   0
	 X =  2.00    Y = -0.5798781411E+00  0.6090775251E+00    NSTEP =  60
	 X =  4.00    Y = -0.1983335270E+00  0.1137638086E+01    NSTEP =  73
	 X =  6.00    Y = -0.4735743943E+00  0.2239068118E+00    NSTEP =  91
	 X =  8.00    Y = -0.1174553350E+01 -0.2759466982E+00    NSTEP = 110
	 X = 10.00    Y = -0.8398073466E+00  0.4468302268E+00    NSTEP = 122
	 X = 12.00    Y =  0.1314712468E-01 -0.8385751499E+00    NSTEP = 145
	 X = 14.00    Y = -0.6031129504E+00 -0.9912598031E+00    NSTEP = 159
	 X = 16.00    Y =  0.2427110999E+00 -0.3899948833E+00    NSTEP = 177
	 X = XEND     Y =  0.9940021016E+00  0.8911185056E-05
	     tol=0.10D-06   fcn= 1442 step= 240 accpt= 216 rejct= 22

*)
REQUIRE "ivpsystem.a4l";
REQUIRE "atoms.a4l";

(* load the DOPRI5 integrator dynamically *)
IMPORT "johnpye/dopri5/dopri5";
IMPORT "johnpye/extpy/extpy";
IMPORT "johnpye/solvernotes";

MODEL aren;
	(* constants *)
	amu, amup IS_A real_constant;
	amu :== 0.012277471;
	amup :== 1.0 - amu;

	(* variables *)
	r1, r2 IS_A factor;
	y[0..3] IS_A factor;
	dy_dt[0..3] IS_A factor;

	
	r1 = ( (y[0] + amu)^2 + y[1]^2 ) ^ (3./2);
	r2 = ( (y[0] - amup)^2 + y[1]^2 ) ^ (3./2);

	ode0: dy_dt[0] = y[2];
	ode1: dy_dt[1] = y[3];
	ode2: dy_dt[2] = y[0] + 2.0 * y[3] - amup * (y[0]+amu) / r1 - amu * (y[0]-amup) / r2;
	ode3: dy_dt[3] = y[1] - 2.0 * y[2] - amup * y[1] / r1 - amu * y[1] / r2;

	x IS_A time;
METHODS
	METHOD values;
		(* initial values *)
		x := 0.0 {s};
		y[0] := 0.994;
		y[1] := 0.0;
		y[2] := 0.0;
		y[3] := -2.00158510637908252240537862224;
	END values;
	METHOD on_load;
		(* xend = 17.0652165601579625588917206249; *)
		RUN values;
		FOR i IN [0..3] DO
			y[i].ode_id := i+1;
			y[i].ode_type := 1;
			FIX y[i];
			dy_dt[i].ode_id := i+1;
			dy_dt[i].ode_type := 2;
			y[i].obs_id := i+1;
		END FOR;
		x.ode_type := -1;
		CALL solvernotes(SELF);
	END on_load;
END aren;

ADD NOTES IN aren;
	'QRSlv' feastol {1e-12}
END NOTES;


