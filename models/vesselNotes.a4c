(*	ASCEND modelling environment
	Copyright (C) 1998, 2007 Carnegie Mellon University

	This program is free software; you can redistribute it and/or modify
	it under the terms of the GNU General Public License as published by
	the Free Software Foundation; either version 2, or (at your option)
	any later version.

	This program is distributed in the hope that it will be useful,
	but WITHOUT ANY WARRANTY; without even the implied warranty of
	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
	GNU General Public License for more details.
	
	You should have received a copy of the GNU General Public License
	along with this program; if not, write to the Free Software
	Foundation, Inc., 59 Temple Place - Suite 330,
	Boston, MA 02111-1307, USA.
*)
(*
	THIS FILE IS AUTO-IMPORTED INTO THE ASCEND MANUAL! BE CAREFUL WITH EDITS.
	vesselNotes.a4c
	by Arthur W. Westerberg
 *)
REQUIRE "atoms.a4l";

MODEL vessel;
    NOTES
	  'author' SELF {Arthur W. Westerberg}
	  'creation date' SELF {May, 1998}
    END NOTES;

	(* variables *)
	side_area	"the area of the cylindrical side wall of the vessel",
	end_area	"the area of the flat ends of the vessel"
		IS_A area;

	vessel_vol	"the volume contained within the cylindrical vessel",
	wall_vol	"the volume of the walls for the vessel"
		IS_A volume;

	wall_thickness	"the thickness of all of the vessel walls",
	H		"the vessel height (of the cylindrical side walls)",
	D		"the vessel diameter"
		IS_A distance;

	H_to_D_ratio	"the ratio of vessel height to diameter"
		IS_A factor;

	metal_density	"density of the metal from which the vessel
			 is constructed"
		IS_A mass_density;

	metal_mass	"the mass of the metal in the walls of the vessel"
		IS_A mass;

	(* equations *)
    FlatEnds:      end_area = 1{PI} * D^2 / 4;
    Sides:         side_area = 1{PI} * D * H;
    Cylinder:      vessel_vol = end_area * H;
    Metal_volume:  (side_area + 2 * end_area) * wall_thickness = wall_vol;
    HD_definition: D * H_to_D_ratio = H;
    VesselMass:    metal_mass = metal_density * wall_vol;
END vessel;

ADD NOTES IN vessel;
    'description' SELF {This model relates the dimensions of a
	   cylindrical vessel -- e.g., diameter, height and wall thickness
	   to the volume of metal in the walls.  It uses a thin wall
	   assumption -- i.e., that the volume of metal is the area of
	   the vessel times the wall thickness.}
    'purpose' SELF {to illustrate the insertion of notes into a model}
END NOTES;
