REQUIRE "atoms.a4l";

MODEL electron_stream;
	V IS_A voltage;
	I IS_A current;
END electron_stream;

MODEL equipment;
	inlet "in:" IS_A electron_stream;
	outlet "out:" IS_A electron_stream;
	(*inout "inout" IS_A electron_stream;*)
END equipment;

MODEL tee REFINES equipment;
	NOTES
		'block' SELF {Tee piece}
		'icon' SELF {tee.svg}
		'graphic' SELF {0,5-4,5
			4,5-10,0
			4,5-10,10}
	END NOTES;
	side "out:" IS_A electron_stream;
	inlet.I = outlet.I + side.I;
	inlet.V, outlet.V, side.V ARE_THE_SAME;
END tee;

MODEL branch REFINES equipment;
	NOTES
		'block' SELF {Branch}
		'icon' SELF {branch.svg}
		'graphic' SELF {0,0-6,5
			0,10-6,5
			6,5-10,5}
		'port_in' SELF {inlet:0,0-side:0,10}
		'port_out' SELF {outlet:10,5}
	END NOTES;
	side "in:" IS_A electron_stream;
	inlet.I + side.I = outlet.I;
	inlet.V, outlet.V, side.V ARE_THE_SAME;
END branch;

MODEL DC_Voltage_Source REFINES equipment;
	NOTES
		'block' SELF {DC Voltage Source}
		'icon' SELF {dcvoltagesource.svg}
		'graphic' SELF {0,5-4,5
			4,3.5-4,6.5
			6,0-6,10
			6,5-10,5}
	END NOTES;
	Voltage "param: DC Voltage supplied by the DC Voltage Source" IS_A voltage;
	inlet.V + Voltage = outlet.V;
	inlet.I = outlet.I;
END DC_Voltage_Source;

MODEL resistor_basic REFINES equipment;
	NOTES
		'block' SELF {Basic Resistor}
		'icon' SELF {resistor.svg}
		'graphic' SELF {0,5-2,5
			2,5-2.5,8
			2.5,8-3.5,2
			3.5,2-4.5,8
			4.5,8-5.5,2
			5.5,2-6.5,8
			6.5,8-7.5,2
			7.5,2-8,5
			8,5-10,5}
	END NOTES;
	Resistance "param: Resistance of the resisitor" IS_A resistance;
	inlet.I = outlet.I;
	outlet.V = (inlet.I * Resistance) + inlet.V;
END resistor_basic;

MODEL Ground_source;
	NOTES
		'block' SELF {Ground Source}
		'icon' SELF {reservoir_in.png}
		'graphic' SELF {5,0-5,5
			0,5-10,5
			2,7-8,7
			4,9-6,9}
		'port_out' SELF {outlet:5,0}
	END NOTES;
	outlet "out:" IS_A electron_stream;
	outlet.V = 0 {volt};
END Ground_source;

MODEL Ground_sink;
	NOTES
		'block' SELF {Ground Sink}
		'icon' SELF {reservoir_in.png}
		'graphic' SELF {5,0-5,5
			0,5-10,5
			2,7-8,7
			4,9-6,9}
		'port_in' SELF {inlet:5,0}
	END NOTES;
	inlet "in:" IS_A electron_stream;
	inlet.V = 0 {volt};
END Ground_sink;
