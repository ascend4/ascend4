IMPORT "ipopt";
REQUIRE "atoms.a4l";
(*
This is the sample problem given in the IPOPT documentation:
http://www.coin-or.org/Ipopt/documentation/node28.html
*)
MODEL test15;

	x[1..4] IS_A solver_var;

	MINIMIZE x[1]*x[4]*(x[1] + x[2] + x[3]) + x[3];

	PROD[x[i] | i IN [1..4]] > 25.;

	SUM[x[i]^2 | i IN [1..4]] = 40.;

METHODS
METHOD on_load;

	x[1..4].lower_bound := 1;
	x[1..4].upper_bound := 5;

END on_load;
METHOD self_test;
	ASSERT abs(x[1] - 1.) < 1e-8;
	ASSERT abs(x[2] - 4.743) < 1e-3;
	ASSERT abs(x[3] - 3.82114998) < 1e-4;
	ASSERT abs(x[4] - 1.37940829) < 1e-4;
END self_test;
END test15;


