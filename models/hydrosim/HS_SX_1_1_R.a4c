REQUIRE "hydrosim/HydroSim.a4l";

(*
   
   by Luis Cisternas & Colaborators

   This file is part of Ascend Modeling Library 
   for Solvent Extraction Modeling.
   
   This code illustrate the modeling of a solvent 
   extraction circuit (rigorous) of copper. The circuit has 1
   extraction and 1 stripping stages. 


                        ----------
             ----------|    hp    |<------
             |          ----------        |
             |                            |
             |                            |
             |         -----------        |
             -------->|    ex1    |-------
             ---------|           |<------
             |         -----------        |
             v                            |
        -----------                       |
   <---|    sp1    |                      |
        -----------                       |
             |                            |
             |         -----------        |
             -------->|    st1    |-------
             ---------|           |<------
             |         -----------        |
             |                            |
             |         -----------        |
             -------->|    ew     |-------
                       -----------        

 
*)
(*
$Date:04/11/05 $
$Revision: 1.2 $
$Authors: Cisternas, Galvez $
*)


MODEL flowsheet;

   hp          IS_A heap_leaching;
   ex1,st1     IS_A mixersettler_rigorous;
   ew          IS_A ew_cell;
   sp1         IS_A splitter;
   efi         IS_A factor;
   ex1.typ :== 'extraction' ;
   st1.typ :== 'stripping' ;
   ex1.hydrodynamic :== 'ContinuidadAqueous' ;
   st1.hydrodynamic :== 'ContinuidadAqueous' ;
   efi=hp.P['Cu']/hp.pls.F['Cu'];
   sp1.n_outputs :== 2;

   ew.spent,st1.aqfeed           ARE_THE_SAME;
   ew.strong,st1.aqout           ARE_THE_SAME;
   st1.orfeed,sp1.out[1]         ARE_THE_SAME;
   ex1.orout,sp1.feed	         ARE_THE_SAME;
   st1.orout,ex1.orfeed          ARE_THE_SAME;
   hp.pls,ex1.aqfeed             ARE_THE_SAME;
   hp.reff,ex1.aqout             ARE_THE_SAME; 


METHODS

    METHOD clear;
	RUN hp.clear;
	RUN ex1.clear;
	RUN st1.clear;
	RUN ew.clear;
      RUN sp1.clear;
    END clear;

    METHOD seqmod;
    END seqmod;

    METHOD specify;
      RUN ex1.seqmod_circuit;
      RUN st1.seqmod_circuit;
      RUN ex1.orfeed.specify_special;
    END specify;

    METHOD reset;
	RUN clear;
	RUN specify;
    END reset;

    METHOD scale;
    END scale;

END flowsheet;


(***********************************************************)

MODEL HS_SX_1_1_R REFINES flowsheet; (* renamed from test_flowsheet *)

METHODS 

  METHOD values;
     ex1.ma['Cu'] := 0.00377 {cm/s} ;
     ex1.ma['Fe'] := 0.000614 {cm/s} ;
     ex1.mo['Fe'] := 0.00303 {cm/s} ;
     ex1.kr['Cu'] := 0.42 {cm/s} ;
     ex1.kr['Fe'] := 0.0058 {cm/s} ;
     ex1.sig := 0.02 {N/m} ;
     ex1.Nag := 125 {1/min} ;
     ex1.D := 0.6 {m} ;
     ex1.do := 0.83 {g/cm^3} ;
     ex1.da := 1.15 {g/cm^3} ;
     ex1.ua := 1.02{cP} ;
     ex1.uo := 2.0 {cP} ;   
     ex1.Vm := 2.8 {m^3} ;
     st1.ma['Cu'] := 0.00377 {cm/s} ;
     st1.ma['Fe'] := 0.000614 {cm/s} ;
     st1.mo['Fe'] := 0.00303 {cm/s} ;
     st1.kr['Cu'] := 0.42 {cm/s} ;
     st1.kr['Fe'] := 0.0058 {cm/s} ;
     st1.sig := 0.02 {N/m} ;
     st1.Nag := 125 {1/min} ;
     st1.D := 0.6 {m} ;
     st1.do := 0.83 {g/cm^3} ;
     st1.da := 1.15 {g/cm^3} ;
     st1.ua := 1.02{cP} ;
     st1.uo := 2.0 {cP} ;   
     st1.Vm := 2.8 {m^3} ;
     ex1.aqfeed.state.w['Cu'] := 1.96 {g/l};
     ex1.aqfeed.state.w['Fe'] := 8.54 {g/l};
     ex1.aqfeed.state.z       := 0.0167 {mole/meter^3};
     ex1.aqfeed.Q             := 12.02 {m^3/hr} ;
     st1.aqfeed.state.w['Cu'] := 46.7 {g/l};
     st1.aqfeed.state.w['Fe'] := 0.03  {g/l};
     st1.aqfeed.state.z       := 1.6326 {mole/meter^3}; 
     st1.aqfeed.Q             := 12.02 {m^3/hr};
     ex1.orfeed.Q             := 13.00 {m^3/hr} ;
  END values;

METHOD on_load;
	RUN reset;
	RUN values;
END on_load;

END HS_SX_1_1_R;

(***********************************************************)
