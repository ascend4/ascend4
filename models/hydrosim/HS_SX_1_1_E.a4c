REQUIRE "hydrosim/HydroSim.a4l";

(*
   
   by Luis Cisternas & Colaborators

   This file is part of Ascend Modeling Library 
   for Solvent Extraction Modeling.
   
   This code illustrate the modeling of a solvent 
   extraction circuit (equilibria) of copper. The circuit has 1
   extraction and 1 stripping stages. 


                        ----------
             ----------|    hp    |<------
             |          ----------        |
             |                            |
             |                            |
             |         -----------        |
             -------->|    ex1    |-------
             ---------|           |<------
             |         -----------        |
             v                            |
        -----------                       |
   <---|    sp1    |                      |
        -----------                       |
             |                            |
             |         -----------        |
             -------->|    st1    |-------
             ---------|           |<------
             |         -----------        |
             |                            |
             |         -----------        |
             -------->|    ew     |-------
                       -----------        

 
*)
(*
$Date:04/11/05 $
$Revision: 1.2 $
$Authors: Cisternas, Galvez, Montenegro $
*)


MODEL flowsheet;

   hp          IS_A heap_leaching;
   ex1,st1     IS_A mixersettler_equilibria;
   ew          IS_A ew_cell;
   sp1         IS_A splitter;
   sp2         IS_A splitter;
   mx          IS_A mixer;
   efi                 IS_A factor;
   cap                 IS_A mass_rate;

   sp1.n_outputs :== 2;
   sp2.n_outputs :== 2;
   mx.n_inputs :== 2;

   ew.spent,st1.aqfeed           ARE_THE_SAME;
   ew.strong,st1.aqout           ARE_THE_SAME;
   st1.orfeed,sp1.out[1]         ARE_THE_SAME;
   hp.pls,ex1.aqfeed             ARE_THE_SAME;
   hp.reff,ex1.aqout             ARE_THE_SAME;
   sp2.out[2],sp1.feed           ARE_THE_SAME;
   sp2.feed,ex1.orout            ARE_THE_SAME;
   sp2.out[1],mx.feed[1]         ARE_THE_SAME;
   mx.out,ex1.orfeed             ARE_THE_SAME;
   mx.feed[2],st1.orout          ARE_THE_SAME;

   efi=hp.P['Cu']/hp.pls.F['Cu'];
   cap=hp.PW['Cu'] ;

METHODS

    METHOD clear;
	RUN hp.clear;
	RUN ex1.clear;
	RUN st1.clear;
	RUN ew.clear;
      RUN sp1.clear;
      RUN sp2.clear;
      RUN mx.clear;
      efi.fixed := FALSE;
      cap.fixed := FALSE;
    END clear;

    METHOD seqmod;
    END seqmod;

    METHOD specify;
      RUN ex1.seqmod_circuit;
      RUN st1.seqmod_circuit;
      RUN ex1.orfeed.specify_special;
      RUN sp2.seqmod;
    END specify;

    METHOD reset;
	RUN clear;
	RUN specify;
    END reset;

    METHOD scale;
    END scale;

END flowsheet;


(***********************************************************)

MODEL HS_SX_1_1_E REFINES flowsheet; (* renamed from 'test_flowsheet' *)

METHODS 

METHOD values;
     ex1.kr['Cu'] := 0.42 {cm/s} ;
     ex1.kr['Fe'] := 0.0058 {cm/s} ;
     st1.kr['Cu'] := 0.42 {cm/s} ;
     st1.kr['Fe'] := 0.0058 {cm/s} ;
     ex1.db := 0.08 {cm} ;
     st1.db := 0.08 {cm} ;
     ex1.Vm := 2.8 {m^3} ;
     st1.Vm := 2.8 {m^3} ;
     ex1.aqfeed.state.w['Cu'] := 1.96 {g/l};
     ex1.aqfeed.state.w['Fe'] := 8.54 {g/l};
     ex1.aqfeed.state.z       := 0.0167 {mole/meter^3};
     ex1.aqfeed.Q             := 6.04 {m^3/hr} ;
     st1.aqfeed.state.w['Cu'] := 46.7 {g/l};
     st1.aqfeed.state.w['Fe'] := 0.03  {g/l};
     st1.aqfeed.state.z       := 1.6326 {mole/meter^3}; 
     st1.aqfeed.Q             := 6.05 {m^3/hr};
     ex1.orfeed.Q             := 7.22222 {meter^3/hour} ;
     sp2.split[1]             := 0.1 ;
END values;

METHOD on_load;
	RUN reset;
	RUN values;
END on_load;

END HS_SX_1_1_E;

(***********************************************************)
