REQUIRE "atoms.a4l";
REQUIRE "solar/solar_types.a4l";
REQUIRE "johnpye/thermo_types.a4c";
REQUIRE "johnpye/sunpos_db.a4c";

MODEL sunpos_wrapper REFINES sunpos_db;
	METHODS
		METHOD specify;
			FIX t, L_st, L_loc, phi; (* time and location *)
		    FIX beta, gamma; (* surface orientation *)
		END specify;
END sunpos_wrapper;


MODEL absorbed_intensity;

    t IS_A time; (* starting at zero at midnight on 1 Jan *)

    L_st IS_A angle; (* standard meridian for the current time zone *)
    L_loc IS_A angle; (* local longitude *)
    phi IS_A angle; (* latitude, north positive *)

   	gamma IS_A angle; (* surface azimuth angle, south=0, west positive *)
    beta IS_A angle; (* surface inclination, horiz=0, +90deg=vertical *)



	sp IS_A sunpos_wrapper;
	
	L_st, 	sp.L_st   	ARE_THE_SAME;
	L_loc, 	sp.L_loc 	ARE_THE_SAME;
	phi, 	sp.phi 		ARE_THE_SAME;
	t,		sp.t 		ARE_THE_SAME;

	beta, 	sp.beta 	ARE_THE_SAME;
	gamma,	sp.gamma 	ARE_THE_SAME;


    value  IS_A intensity;
	G IS_A intensity;
	Gt IS_A intensity;
	Gd IS_A intensity;
	Gb IS_A intensity;

	Fs IS_A factor;
	Fg IS_A factor;
	Rb IS_A factor;
	r IS_A factor;
	rg IS_A factor;



    value = Gt * r;

    Gt = Gb*Rb + Fs*Gd + Fg*rg*G; 
    G = Gb + Gd;
    Gd = 0.177 * G;

    Fs = (1 + cos(sp.beta))/2;
    Fg = (1 - cos(sp.beta))/2;
    Rb = cos(sp.theta) / cos(sp.theta_z);

    METHODS
		METHOD specify;
			FIX G, rg, r;
			RUN sp.specify;
		END specify;

        METHOD values;
            G := 950 {W/m^2};
            rg := 0.9;
            r := 0.8;
			
			L_st := -90{deg}; (* USA Central time*)
			L_loc := -89.4{deg};
			phi := +43{deg};
			(* t := 32.4375 {d}; *)
			t := 32{d} + 10{h}+30{min};

    		(* surface orientation *)
			beta := 45{deg};
			gamma := 15{deg};
		END values;

		METHOD on_load;
            RUN specify;
            RUN values;
        END on_load;
END absorbed_intensity;

MODEL example_absorbed_intensity REFINES absorbed_intensity;

	METHODS
		METHOD specify;
			RUN absorbed_intensity::specify;
		END specify;

        METHOD values;
            G := 950 {W/m^2};
            rg := 0.9;
            r := 0.8;
			
			L_st := -90{deg}; (* USA Central time*)
			L_loc := -89.4{deg};
			phi := +43{deg};
			(* t := 32.4375 {d}; *)
			t := 32{d} + 10{h}+30{min};

    		(* surface orientation *)
			beta := 45{deg};
			gamma := 15{deg};
		END values;

		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;
END example_absorbed_intensity;
