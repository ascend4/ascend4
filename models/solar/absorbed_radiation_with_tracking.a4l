REQUIRE "atoms.a4l";
REQUIRE "solar/solar_types.a4l";
REQUIRE "johnpye/thermo_types.a4c";
REQUIRE "johnpye/sunpos.a4c";


MODEL sunpos_tracker1 REFINES sunpos; 
	(* The collector is rotated about a horizontal east-west axis with single daily adjustment such that the solar beam is normal to the collector aperture plane at solar noon *)

	cos(beta) * cos(phi - delta) + cos(gamma) * sin(beta) * sin(phi - delta) = 1;

    METHODS
        METHOD specify;         
            FIX t, L_st, L_loc, phi; (* time and location *)
            FIX gamma; (* surface orientation *)
			
			FREE beta;
        END specify;

        METHOD bound_self;
            beta.lower_bound := 0 {deg};
            beta.upper_bound := 90 {deg};
        END bound_self;

        METHOD values;
            L_st := -90{deg};    (* USA Central time*)
            L_loc := -89.4{deg};
            phi := +43{deg};     (* t := 32.4375 {d}; *)
            t := 32{d} + 10{h}+30{min};

            (* surface orientation *)
            gamma := 15{deg};
		END values;

		METHOD default_self;
            beta := 45{deg};
        END default_self;

        METHOD on_load;
            RUN specify;
            RUN bound_self;		
            RUN values;
			RUN default_self;
        END on_load;
END sunpos_tracker1;


MODEL sunpos_tracker2 REFINES sunpos;
	(* The collector is rotated about a horizontal east-west axis with continuous adjustment to minimize the angle of incidence *)
	
	sin(delta) * ( sin(phi)*sin(beta) + cos(phi)*cos(beta)*cos(gamma) ) = cos(delta) * cos(omega) * ( sin(phi) * cos(beta) * cos(gamma) - cos(phi) * sin(beta) ) + cos(delta) * cos(beta) * sin(gamma) * sin(omega);

	METHODS
		METHOD specify;
			FIX t, L_st, L_loc, phi; (* time and location *)
		    FIX gamma; (* surface orientation *)
		END specify;

		METHOD bound_self;
			beta.lower_bound := 0 {deg};
			beta.upper_bound := 90 {deg};
		END bound_self;

		METHOD values;		
			L_st := -90{deg};
			L_loc := -89.4{deg};
			phi := +43{deg};		
				(* t := 32.4375 {d}; *)
			t := 32{d} + 10{h}+30{min};
			gamma := 15{deg};  		(* surface orientation *)
		END values;

		METHOD default_self;
			beta := 45{deg};
		END default_self;

		METHOD on_load;
            RUN specify;
            RUN values;
			RUN bound_self;
			RUN default_self;
        END on_load;
END sunpos_tracker2;


MODEL absorbed_intensity;
	gamma IS_A angle; (* surface azimuth angle, south=0, west positive *)
	beta IS_A angle; (* surface inclination, horiz=0, +90deg=vertical *)
	theta IS_A angle; (* angle of incidence of sun onto inclined surface *)
	theta_z IS_A angle; (* zenith angle of the sun *)

	S  IS_A intensity; (* Absorbed Intensity *)
	
	G IS_A intensity;
	Gt IS_A intensity;
	Gd IS_A intensity;
	Gb IS_A intensity;

	Fs IS_A factor;
	Fg IS_A factor;
	Rb IS_A factor;
	r IS_A factor;
	rg IS_A factor;


    S = Gt * r;

    Gt = Gb*Rb + Fs*Gd + Fg*rg*G; 
    G = Gb + Gd;
    Gd = 0.177 * G;

    Fs = (1 + cos(beta))/2;
    Fg = (1 - cos(beta))/2;
    Rb = cos(theta) / cos(theta_z);

    METHODS
		METHOD specify;
			FIX G, rg, r;
		END specify;
END absorbed_intensity;


MODEL absorbed_intensity_tracking_method_1;

	sp IS_A sunpos_tracker1;
	abs IS_A absorbed_intensity;

	S  IS_A intensity; (* Absorbed Intensity *)

	S,				abs.S		ARE_THE_SAME;
	abs.beta, 		sp.beta 	ARE_THE_SAME;
	abs.gamma,		sp.gamma 	ARE_THE_SAME;
	abs.theta, 		sp.theta 	ARE_THE_SAME;
	abs.theta_z,	sp.theta_z 	ARE_THE_SAME;


    METHODS
		METHOD specify;
			RUN abs.specify;
			RUN sp.specify;
		END specify;

		METHOD bound_self;
			RUN sp.bound_self;
		END bound_self;

		METHOD default_self;
			RUN sp.default_self;
		END default_self;

		METHOD values;
            abs.G := 950 {W/m^2};
            abs.rg := 0.9;
            abs.r := 0.8;
			
			sp.L_st := -90{deg}; (* USA Central time*)
			sp.L_loc := -89.4{deg};
			sp.phi := +43{deg};
				(* t := 32.4375 {d}; *)
			sp.t := 32{d} + 10{h}+30{min};

    		(* surface orientation *)
			sp.gamma := 15{deg};
		END values;

		METHOD on_load;
            RUN specify;
			RUN bound_self;
			RUN default_self;
            RUN values;			
        END on_load;

END absorbed_intensity_tracking_method_1;


MODEL absorbed_intensity_tracking_method_2;

	sp IS_A sunpos_tracker2;
	abs IS_A absorbed_intensity;

	S  IS_A intensity; (* Absorbed Intensity *)

	S,				abs.S		ARE_THE_SAME;
	abs.beta, 		sp.beta 	ARE_THE_SAME;
	abs.gamma,		sp.gamma 	ARE_THE_SAME;
	abs.theta, 		sp.theta 	ARE_THE_SAME;
	abs.theta_z,	sp.theta_z 	ARE_THE_SAME;


    METHODS
		METHOD specify;
			RUN abs.specify;
			RUN sp.specify;
		END specify;

		METHOD bound_self;
			RUN sp.bound_self;
		END bound_self;

		METHOD default_self;
			RUN sp.default_self;
		END default_self;

		METHOD values;
            abs.G := 950 {W/m^2};
            abs.rg := 0.9;
            abs.r := 0.8;
			
			sp.L_st := -90{deg}; (* USA Central time*)
			sp.L_loc := -89.4{deg};
			sp.phi := +43{deg};
				(* t := 32.4375 {d}; *)
			sp.t := 32{d} + 10{h}+30{min};

    		(* surface orientation *)
			sp.gamma := 15{deg};
		END values;

		METHOD on_load;
            RUN specify;
			RUN bound_self;
			RUN default_self;
            RUN values;			
        END on_load;

END absorbed_intensity_tracking_method_2;
