REQUIRE "steam/iapwssatprops.a4c";

MODEL iapwssat REFINES iapwssatprops;
	x IS_A fraction; (* ensures we stay in the required region *)
	h IS_A specific_enthalpy;
	u IS_A specific_energy;
	v IS_A specific_volume;

	h_expr: h = (h_g - h_f)*x + h_f;
	u_expr: u = (u_g - u_f)*x + u_f;
	v_expr: v = (rho_g + (rho_f - rho_g)*x ) / ( rho_f * rho_g );
METHODS
METHOD default_self;
	x := 0.5;
	h := 3000 {kJ/kg};
	u := 2300 {kJ/kg};
	v := 100 {m^3/kg};
	RUN iapwssatprops::default_self;
END default_self;
METHOD specify;
	RUN iapwssatprops::specify;
	FIX x;
END specify;
METHOD values;
	RUN iapwssatprops::values;
	x := 0.5;
END values;
METHOD on_load;
	RUN default_self;
	RUN reset; RUN values;
	RUN bound_self;
END on_load;
END iapwssat;

MODEL testiapwssat1 REFINES iapwssat;
METHODS
METHOD values;
	RUN iapwssatprops::values;
	x := 1.01;
	x.upper_bound := 1.0;
	x.lower_bound := 0.0;
END values;
METHOD self_test;
	ASSERT x <= x.upper_bound;
	ASSERT x >= x.lower_bound;
END self_test;
END testiapwssat1;
