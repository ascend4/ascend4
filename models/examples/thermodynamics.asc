(*********************************************************************\
                        thermodynamics.asc
                        by Joseph J. Zaher
                        Part of the Ascend Library

This file is part of the Ascend modeling library.

Copyright (C) 1994

The Ascend modeling library is free software; you can redistribute
it and/or modify it under the terms of the GNU General Public License as
published by the Free Software Foundation; either version 2 of the
License, or (at your option) any later version.

The Ascend Language Interpreter is distributed in hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License along with
the program; if not, write to the Free Software Foundation, Inc., 675
Mass Ave, Cambridge, MA 02139 USA.  Check the file named COPYING.

Use of this module is demonstrated by the associated script file
thermodynamic.s.
\*********************************************************************)

(*********************************************************************\
  $Date: 1997/02/20 18:54:29 $
  $Revision: 1.5 $
  $Author: mthomas $
  $Source: /afs/cs.cmu.edu/project/ascend/Repository/models/examples/thermodynamics.asc,v $
\*********************************************************************)
(**
 **
 **  T  H  E  R  M  O  D  Y  N  A  M  I  C  S  .  A  S  C
 **  ----------------------------------------------------
 **
 **  A listing of exemplary ASCEND models for performing 
 **  thermodynamic property calculations using the thermodynamics
 **  library.
 **
 **  Joseph J. Zaher
 **  05/93
 **
 **)


(* REQUIRE thermodynamics.lib *)

MODEL UNIFAC_test;
(**
 **  Ref:   Gmehling, J., P. Rasmussen and A. Fredenslund. (1982). "Vapor-
 **         Liquid Equilibria by UNIFAC Group Contribution, Revision and
 **         Extension 2".  Ind. Eng. Chem. Process Des. Dev., 21, 118-127.
 **
 **  Data:  This model will compute the minimum boiling azeotrope near the
 **         literature values of
 **
 **            state.y['C3H6O'] := 0.62
 **            state.y['nC6H14'] := 0.38
 **)
    state                                 IS_A equilibrium_mixture;
    state.components                      :== ['C3H6O', 'nC6H14'];
    state.data['nC6H14']                  IS_REFINED_TO n_hexane;
    state.data['C3H6O']                   IS_REFINED_TO acetone;
    state.phases                          :== ['vapor', 'liquid'];
    state.reference                       :== 'liquid';
    state.mix['vapor']                    IS_REFINED_TO Pitzer_mixture;
    state.mix['liquid']                   IS_REFINED_TO UNIFAC_mixture;

    METHODS
        METHOD values;
            state.T := 293.15{K};
            state.P := 135.0{mmHg};
            state.alpha['vapor'][state.components] := 1.0;
            state.phi['liquid'] := 1.0;
        END values;
        METHOD clear;
            RUN state.clear;
        END clear;
        METHOD specify;
            RUN state.specify;
            state.T.fixed := TRUE;
            state.P.fixed := FALSE;
            state.alpha['vapor'][CHOICE[state.components]].fixed := TRUE;
            state.y[state.components].fixed := FALSE;
        END specify;
        METHOD reset;
            RUN clear;
            RUN specify;
        END reset;

END UNIFAC_test;


MODEL Pitzer_test;
(**
 **  Ref:   Voorhies, A. and P. Bryant. (1968). "Hydroisomerization of Normal
 **         Pentane over a Zeolite Catalyst".  AIChE Journal, 14(6), 852-856.
 **
 **  Data:  This model will compute the equilibrium conversion composition 
 **         near the literature values of
 **
 **             state.y['iC5H12'] := 0.691
 **             state.y['nC6H12'] := 0.309  
 **)
    state                                     IS_A Pitzer_mixture;
    state.components                          :== ['iC5H12', 'nC5H12'];
    state.data['iC5H12']                      IS_REFINED_TO i_pentane;
    state.data['nC5H12']                      IS_REFINED_TO n_pentane;

        state.partial['iC5H12'].G, state.partial['nC5H12'].G ARE_THE_SAME;

    METHODS
        METHOD values;
            state.T := 1000.0{R};
            state.P := 465.0{psia};
        END values;
        METHOD clear;
            RUN state.clear;
        END clear;
        METHOD specify;
            RUN state.specify;
            state.y[state.components].fixed := FALSE;
        END specify;
        METHOD reset;
            RUN clear;
            RUN specify;
        END reset;

END Pitzer_test;


MODEL Rackett_test;
(**
 **  Ref:   Smith, J. M. and H. C. Van Ness, (1975). "Introduction to 
 **         Chemical Engineering Thermodynamics, 3rd ed.".  McGraw-Hill.  
 **         Example 6-2, pp 182-183.
 **
 **  Data:  This model will compute the enthalpy and entropy of pure liquid
 **         isobutane at 190{F} and 1500{psia} near the literature values of
 **
 **             state.H := 4539.48 BTU/lb_mole
 **             state.G := -180.70 BTU/lb_mole 
 **)
    state                            IS_A Rackett_component;
    state.data                       IS_REFINED_TO i_butane;

    METHODS
        METHOD values; 
            state.T := 649.67{R};
            state.P := 1500.0{psia};
        END values;
        METHOD clear;
            RUN state.clear;
        END clear;
        METHOD specify;
            RUN state.specify;
        END specify;
        METHOD reset;
            RUN clear;
            RUN specify;
        END reset;

END Rackett_test;




