REQUIRE "thermodynamics.a4l";

IMPORT "johnpye/extpy/extpy";
IMPORT "zplot";

ATOM vdwa REFINES solver_var
DIMENSION M*L^5/Q^2/T^2
DEFAULT 0.1 {kg*m^5/mole^2/sec^2};
lower_bound := 0.0 {kg*m^5/mole^2/sec^2};
upper_bound := 10.0 {kg*m^5/mole^2/sec^2};
nominal := 0.1 {kg*m^5/mole^5/sec^2};
END vdwa;

ATOM virialC REFINES solver_var
DIMENSION L^6/Q^2
DEFAULT 7000.0 {cm^6/mol^2};
lower_bound := 0.0 {cm^6/mol^2};
upper_bound := 1.0 {m^6/mol^2};
nominal := 7000.0 {cm^6/mol^2};
END virialC;

ATOM virialB REFINES solver_var
DIMENSION L^3/Q
DEFAULT 1.0 {m^3/mol};
lower_bound := -1000.00 {m^3/mol};
upper_bound := 1000.0 {m^3/mol};
nominal := 1.0 {m^3/mol};
END virialB;

MODEL vdw(P WILL_BE pressure;
T WILL_BE temperature;
V WILL_BE molar_volume; 
Z WILL_BE factor; 
data WILL_BE td_component_constants;); 

R IS_A gas_constant;
Pc IS_A pressure;
Tc IS_A temperature;
T_degC IS_A factor;
beta, q,Tr, Pr, omega IS_A factor;

alpha, PSI, OMEGA, sigma, eps IS_A real_constant; 

PSI :== 27.0/64.0; 
OMEGA :== 1.0/8.0; 
sigma :== 0.0; 
eps :== 0.0; 
alpha :== 1.0;

Pc = data.Pc;
Tc = data.Tc;
omega = data.omega;

eq1: Tr = T/Tc;
eq2: Pr = P/Pc;
eq3: T_degC = T/1{K} - 273.15;

eq5: q = PSI*alpha/(OMEGA*Tr);
eq6: beta = OMEGA*Pr/Tr;
eq7: Z = 1.0 + beta - q*beta*(Z - beta)/((Z + eps*beta)*(Z + sigma*beta));
eq8: P*V = Z*R*T;

METHODS
    METHOD default_self;
    RUN ClearAll;
    RUN specify;
    RUN values;
    END default_self;

        METHOD specify;
        P.fixed := TRUE;
        T.fixed := TRUE;
        T_degC.fixed := FALSE;
        END specify;
        METHOD values;
        P := 12.0 {bar};
        T := 298.15 {K};
        Z.lower_bound := 0.0;
        Z.upper_bound := 10.0;
        Z.nominal := 0.1;
        END values;

END vdw;


MODEL testvdw;

c1 IS_A symbol_constant;
c1 :== 'ethylene';
cd IS_A components_data([c1],c1);
Tr, Pr IS_A factor;
P IS_A pressure;
T IS_A temperature;
V IS_A molar_volume; 
Z IS_A factor; 
tvdw IS_A vdw(P,T,V,Z,cd.data[c1]); 

eqTr: Tr = T/cd.data[c1].Tc;
eqPr: Pr = P/cd.data[c1].Pc;

METHODS
    METHOD default_self;
    RUN ClearAll;
    RUN specify;
    RUN values;
    END default_self;

    METHOD specify;
    P.fixed := TRUE;
    V.fixed := FALSE;
    Z.fixed := FALSE;  
    T.fixed := TRUE;
    RUN tvdw.specify;
    END specify;
    METHOD values;
    P := 12.0 {bar};
    T := 298.15 {K};
    RUN tvdw.values;
    END values;

    METHOD on_load;
    RUN default_self;
    END on_load;

    METHOD fancyplot;
    EXTERNAL zplot(SELF);
    END fancyplot;

END testvdw; 
