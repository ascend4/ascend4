REQUIRE "atoms.a4l";
(*
	Solution of the 'toy' problem given in the GAMS documentation at
	http://www.gams.com/docs/example.htm

	Here is a standard algebraic description of the problem, which is to 
	minimize the cost of shipping goods from 2 plants to 3 markets, subject 
	to supply and demand constraints.

	This model doesn't currently agree with the GAMS solution when solving using
	CONOPT. We're still debugging it.
*)

MODEL markets;
	(* canning plants *)
	I IS_A set OF symbol_constant;
	I :== ['Seattle','San Diego'];
	
	(* markets *)
	J IS_A set OF symbol_constant;
	J :== ['New York','Chicago','Topeka'];

	(* plant capacities *)
	A[I] IS_A mass_rate_constant;
	A['Seattle'] :== 350 {kg/d};
	A['San Diego'] :== 600 {kg/d};

	(* market demands *)
	B[J] IS_A mass_rate_constant;
	B['New York'] :== 325 {kg/d};
	B['Chicago'] :== 300 {kg/d};
	B['Topeka'] :== 275 {kg/d};

	(* distances *)
	D[I][J] IS_A distance_constant;
	D['Seattle']['New York'] :== 2500 {mi}; (* thousands of miles *)
	D['Seattle']['Chicago'] :== 1700 {mi};
	D['Seattle']['Topeka'] :== 1800 {mi};
	D['San Diego']['New York'] :== 2500 {mi};
	D['San Diego']['Chicago'] :== 1800 {mi};
	D['San Diego']['Topeka'] :== 1400 {mi};

	(* freight costs *)
	F IS_A cost_per_mass_per_distance_constant;
	F :== 90 {USD/kg} / 1000 {mi};

	C[I][J] IS_A cost_per_mass_constant; (* shipping cost per mass, for each route *)
 	FOR i IN I CREATE
		FOR j IN J CREATE
			C[i][j] :== F * D[i][j] ;
		END FOR;
	END FOR;

	(* variables *)
	X[I][J] IS_A mass_rate;
	Z IS_A cost_per_time;

	(* equations *)
(* Note to John ... You forgot the A[i] *) 
 
	COST: Z = SUM[SUM[C[i][j]*A[i] | i IN I] | j IN J];

	supplyslack[I] IS_A mass_rate;
	FOR i IN I CREATE
		SUPPLY[i]: SUM[X[i][j] | j IN J] = A[i] + supplyslack[i];
	END FOR;

	demandslack[J] IS_A mass_rate;
	FOR j IN J CREATE
		DEMAND[j]: SUM[X[i][j] | i IN I] = B[j] + demandslack[j];
	END FOR;

	MINEXPR: MINIMIZE Z;

METHODS
	METHOD on_load;

(* Note to John : you forgot the time *)
		Z := 0 {USD/d};
	END on_load;
	METHOD self_test;
		ASSERT abs(Z - 153.675 {USD/d}) < 0.001 {USD/d};
		ASSERT abs(X['Seattle']['New York'] - 50 {kg/d}) < 0.1 {kg/d};
		ASSERT abs(X['Seattle']['Chicago'] - 300 {kg/d}) < 0.1 {kg/d};
		ASSERT abs(X['Seattle']['Topeka'] - 0 {kg/d}) < 0.1 {kg/d};
	END self_test;

END markets;
	
