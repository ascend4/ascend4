REQUIRE "atoms.a4l";
(* => atoms.a4l, measures.a4l, system.a4l, basemodel.a4l *)
PROVIDE "vessel.a4c";
(*
 * This file is part of the ASCEND Modeling Library and is released
 * under the GNU Public License as described at the end of this file.
 *)

MODEL vessel;

	(* variables *)
	side_area, end_area	IS_A	area;
	vessel_vol, wall_vol	IS_A	volume;
	wall_thickness, H, D	IS_A	distance;
	H_to_D_ratio		IS_A	factor;
	metal_density		IS_A	mass_density;
	metal_mass		IS_A	mass;

	(* equations *)
FlatEnds:		end_area = 1{PI} * D^2 / 4;
Sides:		side_area = 1{PI} * D * H;
Cylinder:	vessel_vol = end_area * H;
Metal_volume:	(side_area + 2 * end_area) * wall_thickness = wall_vol;
HD_definition:	D * H_to_D_ratio = H;
VesselMass:	metal_mass = metal_density * wall_vol;

METHODS

    METHOD defaults;
	H_to_D_ratio		:=	2;
    END defaults;

    METHOD clear;
        FREE side_area;
        FREE end_area;
        FREE vessel_vol;
        FREE wall_vol;
        FREE wall_thickness;
        FREE H;
        FREE D;
        FREE H_to_D_ratio;
        FREE metal_density;
        FREE metal_mass;
    END clear;

    METHOD specify;
	FIX vessel_vol;
	FIX H_to_D_ratio;
	FIX wall_thickness;
	FIX metal_density;
    END specify;

    METHOD reset;
        RUN clear;
        RUN specify;
    END reset;

    METHOD values;
	vessel_vol		:=	250 {ft^3};
	wall_thickness		:=	5 {mm};
	metal_density		:=	13000 {kg/m^3};
    END values;

END vessel;

MODEL vessel_optimize REFINES vessel;

	cost		IS_A	monetary_unit;
	a		IS_A	cost_per_volume;

    obj1def: cost = a * wall_thickness * (side_area + 2*(4/1{PI})*end_area);

    obj1:       MINIMIZE cost;

    METHODS

    METHOD clear;
        FREE side_area;
        FREE end_area;
        FREE vessel_vol;
        FREE wall_vol;
        FREE wall_thickness;
        FREE H;
        FREE D;
        FREE H_to_D_ratio;
        FREE metal_density;
        FREE metal_mass;
        FREE cost;
        FREE a;
    END clear;

    METHOD specify;
	FIX vessel_vol;
	FIX wall_thickness;
	FIX metal_density;
	FIX a;
    END specify;

    METHOD min_cost;
	RUN clear;
	RUN specify;
    END min_cost;

    METHOD values;
	vessel_vol		:=   250 {ft^3};
	wall_thickness		:=   5 {mm};
	metal_density		:=   13000 {kg/m^3};

	(* a is the cost per cubic foot of metal. rather arbitrary. *)
	a			:=   10 {USD};
    END values;

END vessel_optimize;

(*
 *  vessel.a4c
 *  by Arthur W Westerberg, Benjamin A Allan
 *  Part of the ASCEND Library
 *  $Date: 1998/06/17 19:35:43 $
 *  $Revision: 1.3 $
 *  $Author: mthomas $
 *  $Source: /afs/cs.cmu.edu/project/ascend/Repository/models/vessel.a4c,v $
 *
 *  This file is part of the ASCEND Modeling Library.
 *
 *  Copyright (C) 1998 Carnegie Mellon University
 *
 *  The ASCEND Modeling Library is free software; you can redistribute
 *  it and/or modify it under the terms of the GNU General Public
 *  License as published by the Free Software Foundation; either
 *  version 2 of the License, or (at your option) any later version.
 *
 *  The ASCEND Modeling Library is distributed in hope that it
 *  will be useful, but WITHOUT ANY WARRANTY; without even the implied
 *  warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 *  See the GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with the program; if not, write to the Free Software
 *  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139 USA.
 *)
