REQUIRE "ivpsystem.a4l";
REQUIRE "atoms.a4l";

IMPORT "johnpye/extpy/extpy";
IMPORT "johnpye/solve";

(* Model of simple harmonic motion *)
MODEL shm;
	x IS_A factor;
	dx_dt IS_A factor;
	v IS_A factor;
	dv_dt IS_A factor;
	
	xderiv: dx_dt = v;
	vderiv: dv_dt = -x;
	
	t IS_A time;
METHODS

METHOD values;
	v := 0;
	x := 10;
	t := 0 {s};
END values;

METHOD specify;
	FIX x, v;
END specify;

METHOD ode_init;
	FREE x,v;
	t.ode_type := -1; t := 0 {s};
	dx_dt.ode_id := 1; x.ode_id := 1; dx_dt.ode_type := 2; x.ode_type := 1;
	dv_dt.ode_id := 2; v.ode_id := 2; dv_dt.ode_type := 2; v.ode_type := 1;
	x.obs_id := 1;
	v.obs_id := 2;
	dv_dt.obs_id := 3;
END ode_init;
	
METHOD on_load;
	RUN reset; RUN values;
	EXTERNAL solve(SELF);
	RUN values;
	EXTERNAL solve(SELF);
	RUN ode_init;
END on_load;

END shm;
