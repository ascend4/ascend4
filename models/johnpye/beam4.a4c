REQUIRE "johnpye/beam.a4c";
REQUIRE "johnpye/aiscbeams.a4c";

(*
	Model of a beam with four supports and one central load.

	Uses superposition of deflections to overcome force indeterminancy.
*)
MODEL beam4;
	n IS_A integer_constant;
	n :== 3; (* five superpositions for the four supports and the applied load *) 

	E IS_A youngs_modulus;
	I IS_A second_moment_of_area;
	L IS_A distance;
	c IS_A distance; (* max distance from neutral axis *)
	Ag IS_A area;
	
	aisc IS_A aiscbeams;
	
	designation IS_A symbol;
	
	S IS_A beam_superposition(n,E,I,L);

	P ALIASES S.B[2].P;
	a ALIASES S.B[2	].a;
	x2 ALIASES S.B[1].a;
	x3 ALIASES S.B[3].a;

	v ALIASES S.v[2];

	R1 ALIASES S.R1;
	R2 ALIASES S.B[1].P;
	R3 ALIASES S.B[3].P;
	R4 ALIASES S.R2;

	M IS_A moment;
	M + R3*(S.B[3].a - a) + R4*(L-a) = 0;
	
	sigma_max IS_A stress;
	sigma_max = M*c/I;
	tau_max IS_A stress;
	tau_max = sigma_max / 2; (* no shear here at point of max bending *)
	
	a = L / 2;
	S.B[1].a = L/3;
	S.B[3].a = 2*L/3;
	
METHODS
METHOD specify;
	FIX E,I,L,c,Ag;
	FIX P;
	FIX S.v[1,3];
END specify;

METHOD bound_self;
	L.upper_bound := 500 {m};
	L.lower_bound := 0 {m};
	RUN S.bound_self;
END bound_self;

METHOD beam_lookup;
	I := aisc.Ix[designation];
	c := aisc.d[designation] / 2;
	Ag := aisc.Ag[designation];
END beam_lookup;

METHOD values;
	(* beam properties *)
	E := 200 {GPa};
	designation := '150UB18.0';
	RUN beam_lookup;
	
	L := 9 {m};

	(* load *)
	P := 10 {kN};

	(* deflections at supports *)
	FOR i IN [1,3] DO
		S.v[i] := 0 {m};
	END FOR;

END values;

METHOD on_load;
	RUN reset; RUN bound_self;
	RUN values;
END on_load;

END beam4;
