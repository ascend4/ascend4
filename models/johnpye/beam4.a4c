REQUIRE "johnpye/beam.a4c";

(*
	Model of a beam with four supports and one central load.

	Uses superposition of deflections to overcome force indeterminancy.
*)
MODEL beam4;
	n IS_A integer_constant;
	n :== 3; (* five superpositions for the four supports and the applied load *) 

	E IS_A youngs_modulus;
	I IS_A second_moment_of_area;
	L IS_A distance;
	
	S IS_A beam_superposition(n,E,I,L);

	P ALIASES S.B[2].P;
	a ALIASES S.B[2	].a;

	v ALIASES S.v[2];

	R1 ALIASES S.R1;
	R2 ALIASES S.B[1].P;
	R3 ALIASES S.B[3].P;
	R4 ALIASES S.R2;

METHODS
METHOD specify;
	FIX E,I,L;
	FIX S.B[1,3].a;
	FIX P, a;
	FIX S.v[1,3];
END specify;

METHOD bound_self;
	L.upper_bound := 500 {m};
	L.lower_bound := 0 {m};
	RUN S.bound_self;
END bound_self;

METHOD values;
	(* beam properties *)
	E := 200 {GPa};
	I := 10e6 {mm^4};
	L := 9 {m};

	(* load *)
	P := 10 {kN};
	a := 4.5 {m};

	(* locations of supports *)
	S.B[1].a := 3 {m};
	S.B[3].a := 6 {m};

	(* deflections at supports *)
	FOR i IN [1..n] DO
		S.v[i] := 0 {m};
	END FOR;

END values;

METHOD on_load;
	RUN reset; RUN bound_self;
	RUN values;
END on_load;

END beam4;
