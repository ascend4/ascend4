REQUIRE "atoms.a4l";

(*
	Simple model for working out flow rates that achieve a certain
	flow rate and vice versa.
*)
MODEL reynolds;
	Vdot IS_A volume_rate;
	mdot IS_A mass_rate;
	rho IS_A mass_density;
	mu IS_A viscosity;
	Vel IS_A speed;
	A IS_A area;

	circ_area: A = 1{PI} * D^2 / 4.;

	vel_expr: Vel = Vdot / A;
	D IS_A distance;
	
	Re_D IS_A factor;

	re_expr: Re_D = rho*Vel*D / mu;

	mdot_expr: mdot = rho * Vdot;

METHODS
METHOD on_load;
	FIX D;
	D := 10.26 {mm};
	FIX rho;
	rho := 977.3 {kg/m^3};
	FIX mu;
	mu := 4.01e-4 {Pa*s};
	FIX Re_D;
	Re_D := 1000;
END on_load;

END reynolds;

(*
	Model for flow rates and Reynolds numbers in annular channels.
*)
MODEL reynolds_annulus;
	Vdot IS_A volume_rate;
	mdot IS_A mass_rate;
	rho IS_A mass_density;
	mu IS_A viscosity;
	Vel IS_A speed;
	A IS_A area;
	P IS_A distance;

	D_i, D_o IS_A distance;

	flow_area: A = 0.25{PI} * D_o^2 - 0.25{PI} * D_i^2;

	wetted_perim: P = 1{PI} * (D_o + D_i);

	hyd_dia: D = 4*A / P;

	vel_expr: Vel = Vdot / A;
	D IS_A distance;
	
	Re_D IS_A factor;

	re_expr: Re_D = rho*Vel*D / mu;

	mdot_expr: mdot = rho * Vdot;

METHODS
METHOD on_load;
	FIX D_i, D_o;
	D_i := 12.7 {mm};
	D_o := 22.20 {mm};
	FIX rho;
	rho := 998.6 {kg/m^3};
	FIX mu;
	mu := 1.12e-3 {Pa*s};
	FIX Re_D;
	Re_D := 1000;
END on_load;

END reynolds_annulus;

