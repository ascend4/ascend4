# NB there are TWO scons scripts in this directory. The 'SConscript' one is
# run when 'scons' is launched from higher up the directory tree, eg from
# ~/ascend. The 'SConstruct' file, on the other hand, is run when you launch 
# 'scons' from this directory right here. The latter is here just to prove that
# FPROPS can be built standalone; it doesn't need ASCEND to be present to
# work.

Import('env')
fprops_env = env.Clone()

fprops_env.Append(LIBPATH=['#'])
fprops_env.Append(LIBS=['ascend'])

lib = fprops_env.SharedLibrary("mbwr"
	,['mbwr.c','asc_mbwr.c']
	,SHLIBSUFFIX = env['EXTLIB_SUFFIX']
	,SHLIBPREFIX = env['EXTLIB_PREFIX']
)

# automatically create fluids/fluids_list.h...
fprops_env.SConscript("fluids/SConscript",['fprops_env'])

fprops_env.Depends("fluids.c","fluids/fluids_list.h")

coresrcs = ['ideal.c', 'helmholtz.c', 'sat.c', 'derivs.c', 'solve_ph.c', 'fluids.c']
srcs = coresrcs + fprops_env['fluids']

fprops_env['shobjs'] = [fprops_env.SharedObject(s) for s in srcs]


lib = fprops_env.SharedLibrary("helmholtz"
	,['asc_helmholtz.c'] + fprops_env['shobjs']
	,SHLIBSUFFIX = env['EXTLIB_SUFFIX']
	,SHLIBPREFIX = env['EXTLIB_PREFIX']
)

env['extfns'] += [lib]

if env['WITH_PYTHON']:
	fprops_env.SConscript('python/SConscript',['fprops_env'])
	env['pyfprops'] = fprops_env['pyfprops']

	if env.get('CAN_INSTALL'):
		dir = Dir(env.subst("$INSTALL_ROOT$INSTALL_PYTHON"))
		env.InstallShared(dir,env['pyfprops'])
		env.InstallShared(dir,"python/fprops.py")

# vim: set syntax=python:
