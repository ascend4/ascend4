(* Outline of ASCEND multi-component ideal-solution routine *)
(* To perform the simulation, load the file into ASCEND and run 'setup'. *)

REQUIRE "atoms.a4l";
REQUIRE "johnpye/thermo_types.a4c";
IMPORT "johnpye/fprops/mixture";
REQUIRE "johnpye/fprops/mixtures.a4l";

(* The mixture_test model *)
MODEL mixture_test;
	T,
	T_ext IS_A temperature;
	p IS_A pressure;
	h IS_A specific_enthalpy;

	rho,
	rho_ph1, rho_ph2 IS_A mass_density;

	phases,
	ph1, ph2 IS_A phase_count;
	ph_frac[1..3] IS_A mass_fraction;

	mixt IS_A mixture_spec;
	mixt.npure :== 4;
	mixt.components[1] :== 'ammonia';
	mixt.components[2] :== 'cyclohexane';
	mixt.components[3] :== 'water';
	mixt.components[4] :== 'toluene';
	mixt.eos :== 'pengrob';
	mixt.xs[1] :== 0.30;
	mixt.xs[2] :== 0.25;
	mixt.xs[3] :== 0.35;
	mixt.xs[4] :== 0.10;

	(* -----------------------------------------------------------------
	   Find temperature from enthalpy *)
	mix_temperature : mixture_h(
		T, p : INPUT;
		h : OUTPUT;
		mixt : DATA
	);
	mix_temperature_external : mixture_state_T_ph(
		p, h : INPUT;
		T_ext : OUTPUT;
		mixt : DATA
	);

	(* -----------------------------------------------------------------
	   Find number of phases, mass fractions of phases *)
	mix_phases : mixture_count_phases(
		T, p : INPUT;
		phases, ph_frac[1], ph_frac[2], ph_frac[3] : OUTPUT;
		mixt : DATA
	);

	(* -----------------------------------------------------------------
	   Find phase and component densities *)
	mix_density : mixture_rho(
		T, p : INPUT;
		rho : OUTPUT;
		mixt : DATA
	);
	mix_phase1_density : mixture_phase_rho(
		T, p, ph1 : INPUT;
		rho_ph1 : OUTPUT;
		mixt : DATA
	);
	mix_phase2_density : mixture_phase_rho(
		T, p, ph2 : INPUT;
		rho_ph2 : OUTPUT;
		mixt : DATA
	);

METHODS
METHOD specify;
	FIX h;
	FIX p;
	FIX ph1;
	FIX ph2;
END specify;
METHOD values;
	h  := 1000000 {J/kg};
	p  := 10 {bar};
	ph1 := 1;
	ph2 := 2;
END values;
METHOD options;
	SOLVER QRSlv;
	OPTION convopt 'RELNOM_SCALE';
END options;
METHOD setup;
	RUN specify;
	RUN values;
	RUN options;
END setup;
END mixture_test;
