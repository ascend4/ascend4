(* Outline of ASCEND multi-component ideal-solution routine *)
(* To perform the simulation, load the file into ASCEND and run 'setup'. *)

REQUIRE "atoms.a4l";
REQUIRE "johnpye/thermo_types.a4c";
IMPORT "johnpye/fprops/mixture";
REQUIRE "johnpye/fprops/mixtures.a4l";

(* The mixture_test model *)
MODEL mixture_test;
	T IS_A temperature;
	p IS_A pressure;
	rho IS_A mass_density;
	u IS_A specific_energy;
	u_ph1 IS_A specific_energy;

	nphase IS_A phase_count;
	phase_frac[1..3] IS_A mass_fraction;
	phase_rho[1..3] IS_A mass_density;

	mixt IS_A mixture_spec;
	mixt.npure :== 3;
	mixt.components[1] :== 'ammonia';
	mixt.components[2] :== 'carbondioxide';
	mixt.components[3] :== 'cyclohexane';
	mixt.eos :== 'pengrob';
	mixt.xs[1] :== 0.4;
	mixt.xs[2] :== 0.35;
	mixt.xs[3] :== 0.25;

	ii IS_A phase_count;
	mix_comp IS_A mixture_component;

	(* num_comps IS_A phase_count;
	mix_phase_components : mixture_phase_components(
		T, p, ii : INPUT;
		num_comps : OUTPUT;
		mixt : DATA
	); *)

	mix_density : mixture_rho(
		T, p : INPUT;
		rho : OUTPUT;
		mixt : DATA
	);

	mix_phase_data : mixture_flash_phases(
		T, p : INPUT;
		nphase, phase_frac[1], phase_frac[2], phase_frac[3] : OUTPUT;
		mixt : DATA
	);

	mix_u : mixture_u(
		T, p : INPUT;
		u : OUTPUT;
		mixt : DATA
	);

	mix_phase_u : mixture_phase_u(
		T, p, ii : INPUT;
		u_ph1 : OUTPUT;
		mixt : DATA
	);

METHODS
METHOD specify;
	FIX T;
	FIX p;
	FIX ii;
	FIX mix_comp;
END specify;
METHOD values;
	T  := 300 {K} + 273.15 {K};
	p  := 2 {bar};
	ii := 1;

	mix_comp.grandparent := mixt;
	mix_comp.phase_num   := 1;
	mix_comp.comp_num    := 1;
	mix_comp.T           := T;
	mix_comp.p           := p;
END values;
METHOD options;
	SOLVER QRSlv;
	OPTION convopt 'RELNOM_SCALE';
END options;
METHOD setup;
	RUN specify;
	RUN values;
	RUN options;
END setup;
END mixture_test;

