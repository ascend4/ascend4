REQUIRE "johnpye/fprops/rankine_fprops.a4c";

MODEL pipe_adiabatic REFINES stream_equipment;
	D IS_A distance;
	A IS_A area;
	L IS_A distance;
	eps IS_A distance;

	(* energy equation, ignoring potential energy *)
	A = 0.25{PI}*D^2;
	Vel_in, Vel_out IS_A speed;
	Vel_in * A = mdot * inlet.v;
	Vel_out * A = mdot * outlet.v;
	inlet.h + 0.5*Vel_in^2 = outlet.h + 0.5*Vel_out^2;

	(* pressure drops *)
	Re IS_A positive_factor;
	f_D IS_A positive_factor;
	mu IS_A viscosity;
	Re = Vel_in * D / inlet.v / mu;
	(* Swamee-Jain eqn *)
	f_D = 0.25 / (log10(eps/(D*3.7) +5.74/Re^0.9))^2; (* note: constant f assumed over length, calculated with inlet spec vol *)
	(* momentum equation *)
	(inlet.p - outlet.p) = (0.5 * Vel_in^2 / inlet.v) * f_D * (L/D);

	ke_in, ke_out IS_A specific_energy;
	ke_in = 0.5 * Vel_in^2;
	ke_out = 0.5 * Vel_out^2;

	Vel_sonic_out IS_A speed;
	rho_out IS_A mass_density;
	rho_out = 1 / outlet.v;
	calc_w: fprops_w(
		outlet.T, rho_out : INPUT;
		Vel_sonic_out : OUTPUT;
		outlet.cd : DATA
	);
	T_in_C IS_A delta_temperature;
	T_in_C = inlet.T - 273.15 {K};
	T_out_C IS_A delta_temperature;
	T_out_C = outlet.T - 273.15 {K};
END pipe_adiabatic;

MODEL pipe_adiabatic_test REFINES pipe_adiabatic;
	cd.component :== 'water';
	cd.type :== 'helmholtz';
METHODS
METHOD on_load;
	RUN stream_equipment::default_self;
	FIX D := 0.5 {in} - 2 * 2.769 {mm};
	(*FIX mdot := 0.1 {kg/s};*)
	FIX inlet.p := 35 {bar};
	FIX outlet.p := 20 {bar};
	FIX inlet.T := 370 {K} + 273.15 {K};

	FIX mu := 0.024 {cP};
	FIX eps := 0.09 {mm};
	FIX L := 5 {m};

	(* initial guesses *)
	f_D := 0.0001;
	inlet.h := 4000 {kJ/kg};
	outlet.h := 4000 {kJ/kg};
END on_load;
END pipe_adiabatic_test;

MODEL pipe_sequence_test;
	P1, P2 IS_A pipe_adiabatic;
	P1.outlet, P2.inlet ARE_THE_SAME;
	cd ALIASES P1.cd;
	cd.component :== 'water';	
	cd.type :== 'helmholtz';

	P1.eps, P2.eps ARE_THE_SAME;
METHODS
METHOD on_load;
	RUN P1.default_self;
	RUN P2.default_self;
	FIX P1.eps := 0.09 {mm};
	FIX P1.D := 10 {mm};
	FIX P2.D := 20 {mm};
	FIX P1.L := 5 {m};
	FIX P2.L := 25 {m};
	FIX P1.inlet.p := 35 {bar};
	FIX P1.inlet.T := 370 {K} + 273.15 {K};
	FIX P1.mdot := 0.1 {kg/s};

	FIX P1.mu := 0.024 {cP};
	FIX P2.mu := 0.020 {cP};

	P1.inlet.h := 4000 {kJ/kg};
	P1.outlet.h := 4000 {kJ/kg};

END on_load;
END pipe_sequence_test;
