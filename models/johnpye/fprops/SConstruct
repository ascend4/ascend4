# Standalone 'SConstruct' to demonstrate how you can build
# this ASCEND external library against an existing copy of
# ASCEND installed on your system.

env = Environment()

if WhereIs('ascend-config'):
	env.ParseConfig(['ascend-config','--libs','--cppflags'])
else:
	print "ERROR: no ascend-config in path"
	env.Append(
		LIBS=["ascend"]
		,LIBPATH=["#/../../.."]
		,CPPPATH=["#/../../.."]
	)

env.Append(
	CFLAGS=['-g']
)

if WhereIs('gsl-config'):
	env.ParseConfig(['gsl-config','--libs','--cflags'])

srcs = ['mbwr.c','asc_mbwr.c']

env.SharedLibrary('mbwr_ascend',srcs)

env['fluids'] = ['ammonia','water','nitrogen','hydrogen','carbondioxide','methane']

coresrcs = ['ideal.c', 'helmholtz.c', 'sat.c', 'sat3.c','derivs.c', 'nonsolver.c', 'solve_ph.c']

srcs = coresrcs + ["%s.c" % f for f in env['fluids']]

env['shobjs'] = [env.SharedObject(s) for s in srcs]

env.SharedLibrary('helmholtz_ascend',env['shobjs'] + ['asc_helmholtz.c'])

env.SConscript('python/SConscript',['env'])

