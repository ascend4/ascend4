REQUIRE "johnpye/shapes.a4c";
REQUIRE "johnpye/thermo_types.a4c";
REQUIRE "johnpye/airprops2.a4c";
REQUIRE "johnpye/fprops/thermophys.a4c";

(*
MODEL hydrogen REFINES thermophysical_fluid_base;
	state IS_A stream_state_thermophysical;
	cd ALIASES state.cd;
	cd.component :== 'hydrogen';
	cd.type :== 'helmholtz';
	T, state.T ARE_THE_SAME;
	p, state.T ARE_THE_SAME;
	v, state.v ARE_THE_SAME;
	s, state.s ARE_THE_SAME;
	mu, state.mu ARE_THE_SAME;
	k, state.k ARE_THE_SAME;
	rho, state.rho ARE_THE_SAME;

	(* TODO cp, x *)
METHODS
	METHOD default;
		RUN state.default;
		state.dens.included := FALSE; (* redundant equation *)
	END default;	
END hydrogen;
*)

MODEL fluidbed_base;
	T IS_A temperature;
	p IS_A pressure;
	
	fluid IS_A thermophysical_fluid_base;
	T, fluid.T ARE_THE_SAME;
	p, fluid.p ARE_THE_SAME;
		
	chamber IS_A solid_cylinder;
	bed_p "bed (packed)" IS_A solid_cylinder;
	bed_mf "bed (min. fluidisation)" IS_A solid_cylinder;
	chamber.d, bed_p.d, bed_mf.d ARE_THE_SAME;
	
	m_p "mass of particles" IS_A mass;
	rho_p "density of particles" IS_A mass_density;
	d_p "diameter of particles" IS_A distance;
	eps_p "void fraction (packed)" IS_A factor;
	eps_mf "void fraction (min. fluidis.)" IS_A factor;
	
	m_p = rho_p * bed_p.V * (1-eps_p);
	m_p = rho_p * bed_mf.V * (1-eps_mf);
	
	U_mf "superficial velocity for min. fluidisation" IS_A speed;
	Re_mf "Reynolds number at minimum fluidised" IS_A factor;
	Re_mf = d_p * U_mf * fluid.rho / fluid.mu;

	mdot_mf "mass flow rate at min. fluidisation" IS_A mass_rate;
	fluid.rho * chamber.end.A * U_mf = mdot_mf;
	
	psi_s "sphericity" IS_A factor;

	(* equation from Alireza's spreadsheet *)
	(* unk: psi_s * eps^3 = 1./14; *)
	
	(* Gibson et al. equation: https://doi.org/10.1016/j.cherd.2018.05.009 *)
	gibson: (1 - eps_mf) = 12.2 * psi_s^2 * eps_mf^3;
	
	(* Wen and Yu equation -- doesn't solve? *)
	(* wen_yu: U_mf = d_p * (rho_p - fluid.rho) * 1{EARTH_G} / (18 * fluid.mu) 
		* (4.9 + Re_mf ) / (4.9 + 0.4 * Re_mf);*)
	
	U_t "terminal velocity" IS_A speed;
	(* Stokes' law (for small spherical particles, low Re *)	
	stokes: U_t = 2{EARTH_G} * (d_p/2.)^2 * (rho_p - fluid.rho) / (9 * fluid.mu);
	
	(* Richardson and Zaki equation https://doi.org/10.1016/0009-2509(54)85015-9 *)
	n IS_A factor;
	rich_zaki: U_mf = U_t * eps_mf^n;
	
	(* pressure drop is based on equilibrium of forces; gravity on pcls' mass *)
	DP_mf "pressure drop in the bed at min. fluidisation" IS_A delta_pressure;
	DP_mf = -m_p * 1{EARTH_G} / chamber.end.A;
	
	F_grav, F_pres, F_mom IS_A force;
	F_grav = -m_p * 1{EARTH_G};
	F_pres = DP_mf * chamber.end.A;
	F_mom = mdot_mf * U_mf * ( 1 - (p + DP_mf) / p);
	
	(* distributor plate *)

	DP_dp "pressure drop in the distributor plate" IS_A delta_pressure;
	r_DP "ration of distributor plate to bed pressure drop" IS_A factor;
	DP_dp = r_DP * DP_mf; (* Kunii, page. 102 *)
	
	t_plate IS_A distance;
	hole IS_A solid_cylinder;
	t_plate, hole.L ARE_THE_SAME;
	n_holes IS_A factor;
	mdot_hole IS_A mass_rate;
	mdot_hole * n_holes = mdot_mf;
	
	U_hole IS_A speed;
	uhole_rel: mdot_hole = fluid.rho * hole.end.A * U_hole;
	
	Re_t "vessel Reynolds number" IS_A factor;
	Re_t = 	chamber.d * U_mf * fluid.rho / fluid.mu;
	
	C_D "drag coefficient for holes" IS_A factor;
	CD_rel: -DP_dp = 0.5 * C_D * fluid.rho * U_hole^2;
	
	(* stirring effect, Kunii, p. 106 *)
	alpha IS_A factor; 
	alpha * -DP_mf * 1{EARTH_G} = 0.5 * fluid.rho * U_hole^2; (* DIMENSIONALITY ERROR HERE *)
	
	(* Stewart and Davidson onset of slugging https://doi.org/10.1016/0032-5910(67)80014-7 *)
	U_sf IS_A speed;
	U_sf - U_mf = 0.2 * 0.35 * (1{EARTH_G} * chamber.d)^0.5;
	(* =also referenced in Baeyens & Geldart https://doi.org/10.1016/0009-2509(74)85051-7 *)
	
END fluidbed_base;

MODEL fluidbed_test_air REFINES fluidbed_base;
	fluid IS_REFINED_TO air_fluid;
METHODS	
	METHOD on_load;
		RUN fluid.default;

		U_mf.lower_bound := 0 {m/s};
		U_hole.lower_bound := 0 {m/s};

		FIX n:= 4.65;
		FIX chamber.d := 68 {mm};
		FIX T := 400 {K} + 273.15 {K};
		FIX eps_p := 0.32;
		FIX p := 1 {atm};
		FIX m_p := 400 {g};
		FIX d_p := 500 {um};
		FIX rho_p := 3500 {kg/m^3}; (* effective density from Spreitzer *)
		FIX chamber.L := 500 {mm};
		FIX psi_s := 0.86;
		FIX r_DP := 0.25;
		FIX hole.d := 1 {mm};
		FIX t_plate := 3 {mm};
		FIX C_D := 0.7;
	END on_load;
END fluidbed_test_air;

