REQUIRE "johnpye/thermo_types.a4c";

MODEL iapws_sat_pressure;

	p IS_A pressure;
	T IS_A temperature;
	pc IS_A pressure_constant;
	Tc IS_A temperature_constant;

	Tc :== 647.096 {K};
	pc :== 22.064 {MPa};

	tau IS_A factor;
	tau_eq: tau = 1 - (T/Tc);

	sat_pressure_curve: ln(p/pc) = (Tc/T)*( 
		a[1]*tau + a[2]*tau^1.5 + a[3]*tau^3 + a[4]*tau^3.5 + a[5]*tau^4 + a[6]*tau^7.5
	);

	a[1..6] IS_A real_constant;
	a[1] :== -7.85951783;
	a[2] :== 1.84408259;
	a[3] :== -11.7866497;
	a[4] :== 22.6807411;
	a[5] :== -15.9618719;
	a[6] :== 1.80122502;

METHODS
METHOD default_self;
	RUN reset; RUN values;
END default_self;
METHOD specify;
	T.fixed := TRUE;
END specify;
METHOD values;
	T := 373.1243 {K};
END values;
METHOD self_test;
	ASSERT abs(p - 101.325 {kPa}) < 1 {Pa};
END self_test;

END iapws_sat_pressure;

MODEL iapws_sat_density;

	rhof IS_A mass_density;
	rhog IS_A mass_density;
	rhoc IS_A mass_density_constant;
	Tc IS_A temperature_constant;
	T IS_A temperature;
	
	rhoc :== 322 {kg/m^3};
	Tc :== 647.096 {K};

	tau IS_A factor;

	tau = 1 - T/Tc;

	rhof / rhoc =  1 + b[1]*tau^(1/3) + b[2]*tau^(2/3) + b[3]*tau^(5/3) + b[4]*tau^(16/3) + b[5] * tau^(43/3) + b[6]*tau^(110/3);

	rhog / rhoc = exp( c[1]*tau^(2/6) + c[2] * tau^(4/6) +c[3]*tau^(8/6) + c[4] *tau^(18/6) + c[5]*tau^(37/6) + c[6]*tau^(71/6) );

	b[1..6] IS_A real_constant;
	b[1] :== 1.99274064;
	b[2] :== 1.09965342;
	b[3] :== -0.510839303;
	b[4] :== -1.75493479;
	b[5] :== -45.5170352;
	b[6] :== -6.74694450e5;

	c[1..6] IS_A real_constant;
	c[1] :== -2.03150240;
	c[2] :== -2.68302940;
	c[3] :== -5.38626492;
	c[4] :== -17.2991605;
	c[5] :== -44.7586581;
	c[6] :== -63.9201063;

METHODS

METHOD default_self;
	RUN test_1;
END default_self;

METHOD specify;
	T.fixed := TRUE;
END specify;

METHOD values;
	T := 273.16 {K};
END values;

(* the following methods implement the reference data points from the IAPWS supsat.pdf release. *)

METHOD test_1;
	RUN ClearAll;
	RUN specify;
	RUN values;
END test_1;

METHOD test_2;
	RUN ClearAll;
	RUN specify;
	T := 373.1243 {K};
END test_2;

METHOD test_3;
	RUN ClearAll;
	RUN specify;
	T := 647.096 {K};
END test_3;

END iapws_sat_density;
