REQUIRE "johnpye/section.a4c";

(*
	This is a model of column buckling using the secant formula, which makes
	an allowance for load eccentricity.

	The symbols/nomenclature here is taken from E P Popov, 'Engineering Mechanics
	of Solids', Prentice Hall, 1990.

	Note that although this model takes a 'section' as a parameter, it just
	assumes that the buckling will occur in the XY plane (in other words about
	the Z axis).
*)
MODEL columnbuckling_model(
	S WILL_BE section;
);
	P, P_design IS_A force;

	(* cross-section area *)
	A ALIASES S.A;

	(* assume buckling is about the Z axis *)
	I_min ALIASES S.I_z;

	y_min ALIASES S.y_min;
	y_max ALIASES S.y_max;

	(* work out the extreme fibre distance *)
	c_max, c_min IS_A distance;
	c_max = abs(y_max - S.y_c);
	c_min = abs(y_min - S.y_c);

	c IS_A distance;
	(* c = MAX(c_min, c_max) *)
	c = c_min + 0.5*(c_max - c_min + abs(c_max - c_min));

	(* radius of gyration and comes from minimum moment of inertia *)
	r IS_A distance;
	r = sqrt(I_min / A);

	(* load eccentricity *)
	e IS_A distance;

	(* effective length of the column *)
	L IS_A distance;

	(* elastic limit *)
	sigma_max IS_A stress;

	(* secand formula relates elastic limit to the permissible load P *)
	sigma_max = P / A * (1 + e*c/r^2 / cos( L_on_r * sqrt( P / 4 / E / A )) );

	(* Young's modulus *)
	E IS_A youngs_modulus;

	(* factor of safety > 1*)
	n IS_A factor;

	(* design load is less than the permissible load from buckling formula *)
	P = n * P_design;

	(* slenderness ratio: for reference *)
	L_on_r IS_A factor;
	L_on_r = L / r;

END columnbuckling_model;

(* 
	Example of a column buckling problem: solving for the design load
	given a particular column design and factor of safety.
*)
MODEL columnbuckling;

	r IS_A distance;
	t IS_A distance;
	S IS_A tubular_section(r,t);
	C IS_A columnbuckling_model(S);

	e ALIASES C.e;
	P ALIASES C.P;
	L ALIASES C.L;
	E ALIASES C.E;
	A ALIASES C.A;
	n ALIASES C.n;
	L_on_r ALIASES C.L_on_r;
	P_design ALIASES C.P_design;
	sigma_max ALIASES C.sigma_max;

METHODS
	METHOD on_load;
		RUN default_self;
		RUN reset; RUN values;
	END on_load;
	METHOD specify;
		FIX C.E, e, r, t, P, L, n;
	END specify;
	METHOD values;
		C.E := 210 {GPa};
		e := 3 {cm};
		r := 44.45 {mm};
		t := 2.6 {mm};
		P := 10 {kN};
		L := 4 {m};
		n := 2;
	END on_load;
	METHOD set_stress;
		FIX sigma_max; sigma_max := 30 {MPa};
		FREE P;
	END set_stress;		
END columnbuckling;

	
