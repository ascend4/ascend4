REQUIRE "johnpye/iapws_sat_curves.a4c";
REQUIRE "johnpye/iapws95.a4c";

MODEL mixer;
	S_in_1 IS_A thermo_state;
	S_in_2 IS_A thermo_state;
	S_out IS_A thermo_state;

	mdot_in_1, mdot_in_2, mdot_out IS_A mass_rate;

	masscons: mdot_in_1 + mdot_in_2 = mdot_out;
	encons: mdot_in_1*S_in_1.h + mdot_in_2*S_in_2.h = mdot_out*S_out.h;

	S_in_1.p, S_in_2.p, S_out.p ARE_THE_SAME;

METHODS
METHOD default_self;
	RUN reset; RUN values; RUN bound_self;
END default_self;

METHOD specify;
	FIX S_in_1.T, S_in_2.T;
	FIX S_out.p;
	FIX mdot_in_1, mdot_in_2;
END specify;	

METHOD values;
	S_out.p := 1 {bar};
	S_in_1.T := 300 {K};
	S_in_2.T := 320 {K};

	mdot_in_1 := 10 {kg/s};
	mdot_in_2 := 1 {kg/s};
END values;

METHOD bound_self;
	(*pass*)
END bound_self;

END mixer;

MODEL mixer_test;
	M IS_A mixer;

	S1 IS_A iapws95_1phase;
	M.S_in_1, S1 ARE_THE_SAME;
	
	S2 IS_A iapws95_1phase;
	M.S_in_2, S2 ARE_THE_SAME;

	S3 IS_A iapws95_1phase;
	M.S_out, S3 ARE_THE_SAME;

METHODS	
METHOD default_self;
	RUN reset; RUN values; RUN bound_self;
END default_self;

METHOD specify;
	FIX S1.T, S1.rho;
	FIX S2.T;
	FIX M.mdot_in_1, M.mdot_in_2;
END specify;	

METHOD values;
	S1.rho := 997 {kg/m^3};
	S1.T := 300 {K};
	S2.T := 320 {K};

	M.mdot_in_1 := 10 {kg/s};
	M.mdot_in_2 := 1 {kg/s};

	(* free values *)
	S2.delta := 3.09;
	S2.d1 := 2.09;
	S2.rho := S1.rho;
	S2.h := 200 {kJ/kg/K};

	S3.delta := 3.09;
	S3.d1 := 2.09;
	S3.rho := S1.rho;
	S3.T := S1.T;

END values;

METHOD bound_self;
	(*pass*)
END bound_self;

END mixer_test;
