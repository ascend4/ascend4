REQUIRE "johnpye/moltensalt.a4c";
(*
	Model of liquid sodium properties.

	Source data is J. Fink and L. Leibowitz (1995). "Thermodynamic and transport
	properties of sodium liquid and vapor', technical report ANL/RE-95/2, 
	Reactor Engineering Division, Argonne National Laboratories, 
	http://www.ne.anl.gov/eda/ANL-RE-95-2.pdf

	To use these correlations in our models, we integrate (cp dT) to obtain
	enthalpy, and integrate (cp/T dT) to obtain entropy. Integration constants
	are added so that h and s evaluate to zero at T = 273.15 K.

#sympy input eg at http://live.sympy.org/ 
T = symbols("T") 
cp = 1.6582 - 8.4790e-4 * T + 4.4541e-7 * T**2 - 2992.6 * T**-2;
h = integrate(cp,T) 
s = integrate(cp/T,T)
h_ref = h.subs(T,273.15)
s_ref = s.subs(T,273.15)

*)
MODEL liquidsodium_fluid REFINES thermophysical_fluid_base;
	(* as given by Fink and Liebowitz *)
	T_c IS_A temperature_constant;
	T_c :== 2305.7{K};			
	rho_c IS_A mass_density_constant;

	rho_c :== 219.{kg/m^3};

	rho_eq: rho = 1{kg/m^3}*(rho_c + 275.32*(1 - T/T_c) + 511.58*sqrt(1 - T/T_c));

	(* as given by Fink and Liebowitz *)
	cp_eq: cp / 1{kJ/kg/K} = 1.6582 - 8.4790e-4 * (T/1{K}) + 4.4541e-7*(T/1{K})^2 - 2992.6*(T/1{K})^-2;

	(* as given by Fink and Liebowitz *)
	mu_eq: mu = 1{Pa*s}*exp(-6.4406 - 0.3958*ln(T/1{K}) + 556.835{K}/T);

	(* as given by Fink and Liebowitz *)
	k_eq: k / 1{W/m/K} = 124.67 - 0.11381*(T/1{K}) + 5.5226e-5*(T/1{K})^2 - 1.1842e-8*(T/1{K})^3;

	(* reference state is h=0, s=0 at 273.15 K *)

	(* by integration of cp, see above *)
	h_eq: h = 1{kJ/kg}*(1.4847e-7*(T/1{K})^3 - 0.000423950000705408*(T/1{K})^2 + 1.6582*(T/1{K}) + 2992.6/(T/1{K})) - 435.287729061046{kJ/kg};

	(* by integration of cp, see above *)
	s_eq: s = 1{kJ/kg/K}*(2.22705e-7*(T/1{K})^2 - 0.000847900001410815*(T/1{K}) + 1.6582*ln((T/1{K})) + 1496.3/(T/1{K})^2) - 9.10760402403353{kJ/kg/K};

METHODS
METHOD on_load;
	FIX p := 1 {bar};
	FIX T := 700 {K} + 273.15 {K};
END on_load;
END liquidsodium_fluid;

