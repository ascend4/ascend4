REQUIRE "ivpsystem.a4l";
REQUIRE "johnpye/dsg.a4c";

MODEL dsg_array;

	t IS_A time;	
	dz IS_A distance;
	n IS_A integer_constant;
	n :== 3;

	node[1..n] IS_A dsg;
	
	(* design variables shared *)
	rhow ALIASES node[1].rhow;   node[1..n].rhow ARE_THE_SAME;
	eps ALIASES node[1].eps;     node[1..n].eps ARE_THE_SAME;
	D ALIASES node[1].D;         node[1..n].D ARE_THE_SAME;
	D2 ALIASES node[1].D2;       node[1..n].D2 ARE_THE_SAME;
	cw ALIASES node[1].cw;       node[1..n].cw ARE_THE_SAME;
	hw ALIASES node[1].hw;       node[1..n].hw ARE_THE_SAME;
	qdots ALIASES node[1].qdots; node[1..n].qdots ARE_THE_SAME;
	Tamb ALIASES node[1].Tamb;   node[1..n].Tamb ARE_THE_SAME;
	
	(* central difference *)
	FOR i IN [2..(n-1)] CREATE
		z_cd_p[i]:      node[i].dp_dz = (node[i+1].p - node[i-1].p)/(2*dz);
		z_cd_mdot[i]:   node[i].dmdot_dz = (node[i+1].mdot - node[i-1].mdot)/(2*dz);
		z_cd_mdoth[i]:  node[i].dmdoth_dz = (node[i+1].mdoth - node[i-1].mdoth)/(2*dz);
		z_cd_rhovel2[i]:node[i].drhovel2_dz = (node[i+1].rhovel2 - node[i-1].rhovel2)/(2*dz);
		z_cd_ekdot[i]:  node[i].dekdot_dz = (node[i+1].ekdot - node[i-1].ekdot)/(2*dz);
	END FOR;

	(* backward difference *)
	FOR i IN [n] CREATE
		z_bd_p[i]:       node[i].dp_dz = (node[i].p - node[i-1].p)/dz;
		z_bd_mdot[i]:    node[i].dmdot_dz = (node[i].mdot - node[i-1].mdot)/dz;
		z_bd_mdoth[i]:   node[i].dmdoth_dz = (node[i].mdoth - node[i-1].mdoth)/dz;
		z_bd_rhovel2[i]: node[i].drhovel2_dz = (node[i].rhovel2 - node[i-1].rhovel2)/dz;
		z_bd_ekdot[i]:   node[i].dekdot_dz = (node[i].ekdot - node[i-1].ekdot)/dz;
	END FOR;

	(* forward difference *)
	FOR i IN [1] CREATE
		z_fd_p[i]:       node[i].dp_dz = (node[i+1].p - node[i].p)/dz;
		z_fd_mdot[i]:    node[i].dmdot_dz = (node[i+1].mdot - node[i].mdot)/dz;
		z_fd_mdoth[i]:   node[i].dmdoth_dz = (node[i+1].mdoth - node[i].mdoth)/dz;
		z_fd_rhovel2[i]: node[i].drhovel2_dz = (node[i+1].rhovel2 - node[i].rhovel2)/dz;
		z_fd_ekdot[i]:   node[i].dekdot_dz = (node[i+1].ekdot - node[i].ekdot)/dz;
	END FOR;
	
METHODS
METHOD specify;
	(* steady state solution, hopefully... *)
	FIX dz;
	RUN fix_design;
	FOR i IN [1..n] DO
		RUN node[i].fix_temporals;
	END FOR;
END specify;

METHOD fix_design;
	RUN node[1].fix_design;
END fix_design;

METHOD values;
	dz := 1.0 {m};
	t := 0 {s};
	FOR i IN [1..n] DO
		RUN node[i].values;
	END FOR;
END values;

METHOD on_load;
	RUN specify;
	RUN values;
END on_load;

END dsg_array;
