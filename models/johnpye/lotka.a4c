REQUIRE "ivpsystem.a4l";
REQUIRE "atoms.a4l";

(*
	A simple population model using the Lotka-Volterra model.
	
	http://www.stolaf.edu/people/mckelvey/envision.dir/lotka-volt.html
	http://www.ento.vt.edu/~sharov/PopEcol/lec10/lotka.html
*)
MODEL lotka;
	t IS_A time;
	R "rabbit population" IS_A factor;
	F "fox population" IS_A factor;
	
	dR_dt IS_A rate;
	dF_dt IS_A rate;

	r "growth rate of rabbits in absence of foxes" IS_A rate;
	a "fraction of rabbits killed per fox per time" IS_A rate;
	b "production rate of predators per prey eaten" IS_A rate;
	m "natural fox mortality rate" IS_A rate; 
	
	dR_dt =  r*R - a*R*F;
	dF_dt = -m*F + b*R*F ;
METHODS

METHOD on_load;
	RUN default_self;
	RUN reset; RUN values;
	RUN set_obs;
	RUN set_ode;
END on_load;

METHOD default_self;
	R := 1000;
	F := 9;
	dR_dt := 0 {Hz};
	dF_dt := 0 {Hz};
END default_self;

METHOD specify;
	FIX r,a,b,m;
	FIX F,R;
END specify;

METHOD values;
	r	:= 0.1 {1/s};
	a	:= 0.01 {1/s};
	b	:= 0.1 * a;
	m	:= 0.05 {1/s};
	t   := 0{s};
END values;

METHOD set_obs;
	t.obs_id :=1;
	R.obs_id :=3;
	F.obs_id :=4;
END set_obs;

METHOD set_ode; (* this is where the current implementation sux *)
	FREE F,R,dR_dt,dF_dt;

	R.ode_id := 1;   R.ode_type := 1;
	dR_dt.ode_id := 1; dR_dt.ode_type := 2;
	
	F.ode_id := 2; F.ode_type := 1;
	dF_dt.ode_id := 2; dF_dt.ode_type := 2;

	t.ode_type :=-1;
END set_ode;


END lotka;
