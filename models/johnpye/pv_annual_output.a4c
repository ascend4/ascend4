REQUIRE "ivpsystem.a4l";
REQUIRE "johnpye/simple_pv.a4c";

IMPORT "johnpye/datareader/datareader";
REQUIRE "johnpye/thermo_types.a4c";


MODEL tmyparams(
	filename IS_A symbol_constant;
	format IS_A symbol_constant;
	parameters IS_A symbol_constant;
);
END tmyparams;

MODEL pv_annual_output;
	t IS_A time;
	E IS_A energy;
	Gb IS_A irradiance;

	(* this is the solar cell model from above *)
	pv IS_A simple_pv;

	(* E will be the integral of the power from the cell *)
	dEdt ALIASES pv.P;

	(* configure and instantiate the data reader for the Daggett TMY data *)
	params IS_A tmyparams('johnpye/datareader/23161.tm2', 'TMY2', '2');
	filereader:datareader(
		t : INPUT;
		Gb :OUTPUT;
		params : DATA
	);

	Gb, pv.G_T ARE_THE_SAME;

METHODS
METHOD on_load;
	RUN pv.on_load;
	FREE pv.G_T;
	RUN set_ode;
	t := 0 {s};
END on_load;

METHOD set_ode; (* TODO incorporate new DAE syntax from Dante Stroe *)
	FREE E, dEdt;
	FIX t;

	E.ode_id := 1;   E.ode_type := 1;
	dEdt.ode_id := 1; dEdt.ode_type := 2;
	t.ode_type := -1;

	t.obs_id := 1;
	E.obs_id := 2;
	dEdt.obs_id :=3;
END set_ode;

END pv_annual_output;


