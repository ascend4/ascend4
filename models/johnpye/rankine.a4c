REQUIRE "atoms.a4l";
REQUIRE "johnpye/thermo_types.a4c";

IMPORT "freesteam";

(* Model of simple Rankine cycle with boiler, turbine, condenser, pump *)

(*------------------------------------------------------------------------------
  BACKGROUND STUFF
*)

(*
	Thermo properties -- IAPWS-IF97
*)
MODEL steam_state;
	p IS_A pressure;
	h IS_A specific_enthalpy;
	u IS_A specific_energy;
	v IS_A specific_volume;
	T IS_A temperature;
	x IS_A fraction;
	s IS_A specific_entropy;
	mu IS_A viscosity;

	thermo: iapws97_uvTxsmu_ph(
		p,h : INPUT;
		u,v,T,x,s,mu : OUTPUT
	);
METHODS
	METHOD default;
		p := 10{bar};
		p.nominal := 42 {bar};
		v.nominal := 10 {L/kg};
		u := 2000 {kJ/kg};
		T := 400 {K};
		x := 0.8;
	END default;
	METHOD solve;
		EXTERNAL do_solve(SELF);
	END solve;
	METHOD on_load;
		RUN default_all;
		FIX p, h;
	END on_load;
END steam_state;

(* a simple connector that includes calculation of steam properties *)
MODEL steam_node;
	state IS_A steam_state;
	p ALIASES state.p;
	h ALIASES state.h;
	u ALIASES state.u;
	v ALIASES state.v;
	T ALIASES state.T;
	x ALIASES state.x;
	s ALIASES state.s;
	mu ALIASES state.mu;
	mdot IS_A mass_rate;
METHODS
	METHOD default;
		mdot.nominal := 2 {kg/s};
	END default;
	METHOD solve;
		EXTERNAL do_solve(SELF);
	END solve;
	METHOD on_load;
		RUN default_all; RUN reset; RUN values;
		FIX p,h;
	END on_load;
END steam_node;

MODEL steam_equipment;
	inlet IS_A steam_node;
	outlet IS_A steam_node;

	inlet.mdot, outlet.mdot ARE_THE_SAME;
	mdot ALIASES inlet.mdot;
END steam_equipment;

(*------------------------------------------------------------------------------
  PUMP COMPONENT
*)

MODEL pump_simple REFINES steam_equipment;

	dp IS_A delta_pressure;
	inlet.p + dp = outlet.p;

	outlet_is IS_A steam_state;
	outlet_is.p, outlet.p ARE_THE_SAME;

	outlet_is.s, inlet.s ARE_THE_SAME;
	eta IS_A fraction;
	
	eta * (inlet.h - outlet.h) = (inlet.h - outlet_is.h);

	(* work done on the environment, will be negative *)
	Wdot IS_A energy_rate;
	Wdot = eta * mdot * (inlet.h - outlet.h);

	w IS_A specific_energy;
	w = eta * (outlet.h - inlet.h);

END pump_simple;

MODEL pump_simple_test REFINES pump_simple;
	(* no equations here *)
METHODS
METHOD on_load;
	FIX inlet.p;
	FIX inlet.h;
	FIX outlet.p;
	FIX eta;
	FIX mdot;

	inlet.p := 5 {bar};
	inlet.h := 400 {kJ/kg};
	outlet.p := 100 {bar};
	eta := 0.65;
	mdot := 900 {t/d};
END on_load;
END pump_simple_test;

(*------------------------------------------------------------------------------
  TURBINE COMPONENT
*)

MODEL turbine_simple REFINES steam_equipment;

	dp IS_A delta_pressure;
	inlet.p + dp = outlet.p;
	
	outlet_is IS_A steam_state;
	outlet_is.p, outlet.p ARE_THE_SAME;
	outlet_is.s, inlet.s ARE_THE_SAME;

	eta IS_A fraction;
	eta * (inlet.h - outlet_is.h) = (inlet.h - outlet.h);
	
	(* work done on the environment, will be positive *)
	Wdot IS_A energy_rate;
	Wdot = eta * mdot * (inlet.h - outlet.h);

	w IS_A specific_energy;
	w = inlet.h - outlet.h;

END turbine_simple;

MODEL turbine_simple_test REFINES turbine_simple;
	(* no equations here *)
METHODS
METHOD on_load;
	FIX inlet.p;
	FIX inlet.h;
	FIX outlet.p;
	FIX eta;
	FIX mdot;

	inlet.p := 100 {bar};
	inlet.h := 3000 {kJ/kg};
	outlet.p := 5 {bar};
	eta := 0.85;
	mdot := 900 {t/d};
END on_load;
END turbine_simple_test;

(*------------------------------------------------------------------------------
  BOILER COMPONENT
*)

(*
	simple model assumes no pressure drop, but heating losses due to
	flue gas temperature
*)
MODEL boiler_simple REFINES steam_equipment;

	inlet.p, outlet.p ARE_THE_SAME;
	Qdot_fuel IS_A energy_rate;
	Qdot IS_A energy_rate;

	Qdot = mdot * (outlet.h - inlet.h);

	eta IS_A fraction;
	Qdot = eta * Qdot_fuel;
END boiler_simple;

MODEL boiler_simple_test REFINES boiler_simple;
	(* nothing here *)
METHODS
METHOD on_load;
	FIX inlet.p;
	FIX inlet.h;
	FIX eta;
	FIX outlet.h;
	FIX mdot;

	inlet.p := 100 {bar};
	inlet.h := 500 {kJ/kg};

	eta := 0.8;
	outlet.h := 3000 {kJ/kg};
	mdot := 900 {t/d};
END on_load;
END boiler_simple_test;

(*------------------------------------------------------------------------------
  CONDENSER COMPONENT
*)

(*
	this is really simple (fluid props permitting): just work out the heat
	that must be expelled to get the water down to a certain state
*)
MODEL condenser_simple REFINES steam_equipment;

	inlet.p, outlet.p ARE_THE_SAME;
	Qdot IS_A energy_rate;

	Qdot = mdot * (outlet.h - inlet.h);
	
END condenser_simple;

MODEL condenser_simple_test REFINES condenser_simple;
	(* nothing here *)
METHODS
METHOD on_load;
	FIX inlet.p, inlet.x;
	FIX outlet.h;
	FIX mdot;

	inlet.p := 5 {bar};
	inlet.x := 0.95;
	outlet.h := 500 {kJ/kg};
	mdot := 900 {t/d};
END on_load;
END condenser_simple_test;

(*------------------------------------------------------------------------------
  FEEDWATER HEATER
*)

(*
	open heater does not have inlet.mdot==outlet.mdot, so not a refinement
	of 'steam_equipment'.
*)
MODEL heater_open;

	inlet IS_A steam_node;
	inlet_heat IS_A steam_node;
	outlet IS_A steam_node;
	
	inlet_heat.p, inlet.p, outlet.p ARE_THE_SAME;

	(* cons. mass *)
	cons_mass: inlet.mdot + inlet_heat.mdot = outlet.mdot;

	(* cons. energy *)
	cons_en: inlet.mdot * inlet.h + inlet_heat.mdot * inlet_heat.h = outlet.mdot * outlet.h;

END heater_open;

MODEL heater_open_test REFINES heater_open;
	(* nothing here *)
METHODS
METHOD on_load;
	FIX inlet.p, inlet.h;
	inlet.p := 40 {bar};
	inlet.h := 634 {kJ/kg};
	FIX inlet_heat.h;
	inlet_heat.h := 2960 {kJ/kg};
	
	FIX outlet.mdot; 
	outlet.mdot := 900 {t/d};
	
	FIX inlet.mdot;
	inlet.mdot := 700 {t/d};
END on_load;
END heater_open_test;

(*------------------------------------------------------------------------------
  TEE PIECE
*)

(*
	it's not a car :-)
*)
MODEL tee;
	inlet IS_A steam_node;
	outlet IS_A steam_node;
	outlet_branch IS_A steam_node;

	
	inlet.p, outlet.p, outlet_branch.p ARE_THE_SAME;
	inlet.h, outlet.h, outlet_branch.h ARE_THE_SAME;

	(* cons. mass *)
	cons_mass: inlet.mdot = outlet.mdot + outlet_branch.mdot;

	phi IS_A fraction;
	phi * inlet.mdot = outlet_branch.mdot;

END tee;	

(*------------------------------------------------------------------------------
  OVERALL CYCLE
*)

(*
	simplest possible rankine cycle
*)
MODEL rankine;

	BO IS_A boiler_simple;
	TU IS_A turbine_simple;
	CO IS_A condenser_simple;
	PU IS_A pump_simple;

	BO.outlet, TU.inlet ARE_THE_SAME;
	TU.outlet, CO.inlet ARE_THE_SAME;
	CO.outlet, PU.inlet ARE_THE_SAME;
	PU.outlet, BO.inlet ARE_THE_SAME;

	Qdot_loss ALIASES CO.Qdot;

	T_H ALIASES BO.outlet.T;
	T_C ALIASES CO.outlet.T;

	eta IS_A fraction;
	eta * (BO.Qdot_fuel - PU.Wdot) = TU.Wdot;

	eta_carnot IS_A fraction;
	eta_carnot = 1 - T_C / T_H;

	mdot ALIASES TU.mdot;
	x_turb_out ALIASES TU.outlet.x;
METHODS
(* first test case: just some plausible values *)
METHOD specify_1;
	RUN ClearAll;
	FIX PU.inlet.p;
	FIX PU.inlet.h;
	FIX PU.outlet.p;
	FIX BO.outlet.h;
	FIX TU.eta;
	FIX PU.eta;
	FIX BO.eta;
	FIX mdot;
END specify_1;
METHOD values_1;
	PU.inlet.p := 1 {bar};
	PU.inlet.h := 104.9 {kJ/kg};
	PU.outlet.p := 250 {bar};
	BO.outlet.h := 3772 {kJ/kg};
	TU.eta := 0.85;
	PU.eta := 0.65;
	BO.eta := 0.9;
	mdot := 900 {t/d};	
END values_1;
(*
	second test case: numbers from Example 2.1, K Weston, 'Energy Conversion',
	1992, http://www.personal.utulsa.edu/~kenneth-weston/
*)
METHOD specify;
	RUN ClearAll;
	FIX PU.outlet.p;
	FIX BO.outlet.T;
	FIX PU.inlet.p;
	FIX PU.inlet.h;
	FIX TU.eta;
	FIX PU.eta;
	FIX BO.eta;
	FIX mdot;
END specify;
METHOD values;
	PU.outlet.p := 2000 {psi};
	BO.outlet.T := 1460 {R}; BO.outlet.h := 3400 {kJ/kg};
	PU.inlet.p := 1 {psi};
	PU.inlet.h := 69.73 {btu/lbm};
	TU.eta := 1.0;
	PU.eta := 1.0;
	BO.eta := 1.0;
	mdot := 900 {t/d};
END values;
METHOD on_load;
	RUN specify;
	RUN values;
END on_load;
METHOD self_test;
	(* check the results against those from K Weston's book *)
	(* note that we have NOT neglected pump work in this case! *)
	ASSERT abs(eta - 0.4294) < 0.0005;
	ASSERT abs(eta_carnot - 0.6152) < 0.0005;
	ASSERT abs(TU.outlet.x - 0.7736) < 0.0005;
END self_test;
END rankine;

(*------------------------------------------------------------------------------
  REHEAT RANKINE CYCLE
*)
MODEL rankine_reheat;

	BO1 IS_A boiler_simple;
	BO2 IS_A boiler_simple;
	TU1 IS_A turbine_simple;
	TU2 IS_A turbine_simple;
	CO IS_A condenser_simple;
	PU IS_A pump_simple;

	BO1.outlet, TU1.inlet ARE_THE_SAME;
	TU1.outlet, BO2.inlet ARE_THE_SAME;
	BO2.outlet, TU2.inlet ARE_THE_SAME;
	TU2.outlet, CO.inlet ARE_THE_SAME;
	CO.outlet, PU.inlet ARE_THE_SAME;
	PU.outlet, BO1.inlet ARE_THE_SAME;

	BO1.eta, BO2.eta ARE_THE_SAME;

	(* boiler peak temperature is reached for both main and reheat... *)
	BO1.outlet.T, BO2.outlet.T ARE_THE_SAME;

	mdot ALIASES PU.mdot;

	T_H ALIASES BO1.outlet.T;
	T_C ALIASES CO.outlet.T;

	eta IS_A fraction;
	eta * (BO1.Qdot_fuel + BO2.Qdot_fuel - PU.Wdot) = TU1.Wdot + TU2.Wdot;

	eta_carnot IS_A fraction;
	eta_carnot = 1 - T_C / T_H;

METHODS
METHOD on_load;
	FIX BO1.eta;
	BO1.eta := 1.0;
	FIX TU1.eta, TU2.eta;
	TU1.eta := 1.0;
	TU2.eta := 1.0;
	FIX PU.eta;
	PU.eta := 1.0;
	FIX PU.inlet.p;
	PU.inlet.p := 1 {psi};
	FIX PU.inlet.h;
	PU.inlet.h := 69.73 {btu/lbm};
	FIX BO1.outlet.T;
	BO1.outlet.T := 1460 {R};
	BO1.outlet.h := 3000 {kJ/kg}; (* guess *)
	FIX PU.outlet.p;
	PU.outlet.p := 2000 {psi};
	FIX mdot;
	mdot := 900 {t/d};

	(* this value here is what defines the intermediate pressure *)
	FIX TU1.outlet.h;
	TU1.outlet.h := 2800 {kJ/kg};

	TU2.inlet.h := 3000 {kJ/kg}; (* guess *)
END on_load;
END rankine_reheat;

(*------------------------------------------------------------------------------
  REGENERATIVE RANKINE CYCLE
*)
(*
	Add a boiler feedwater heater and two-stage turbine
*)
MODEL rankine_regen;

	BO IS_A boiler_simple;
	TU1 IS_A turbine_simple;
	BL IS_A tee; (* bleed *)
	TU2 IS_A turbine_simple;
	CO IS_A condenser_simple;
	HE IS_A heater_open;
	PU1 IS_A pump_simple;
	PU2 IS_A pump_simple;

	(* main loop *)
	BO.outlet, TU1.inlet ARE_THE_SAME;
	TU1.outlet, BL.inlet ARE_THE_SAME;
	BL.outlet, TU2.inlet ARE_THE_SAME;
	TU2.outlet, CO.inlet ARE_THE_SAME;
	CO.outlet, PU1.inlet ARE_THE_SAME;
	PU1.outlet, HE.inlet ARE_THE_SAME;
	HE.outlet, PU2.inlet ARE_THE_SAME;
	PU2.outlet, BO.inlet ARE_THE_SAME;

	(* bleed stream *)
	BL.outlet_branch, HE.inlet_heat ARE_THE_SAME;
	phi ALIASES BL.phi;
	p_bleed ALIASES TU1.outlet.p;

	mdot ALIASES BO.mdot;

	T_H ALIASES BO.outlet.T;
	T_C ALIASES CO.outlet.T;

	eta IS_A fraction;
	eta * (BO.Qdot_fuel + abs(PU1.Wdot) + abs(PU2.Wdot)) = TU1.Wdot + TU2.Wdot;

	eta_carnot IS_A fraction;
	eta_carnot = 1 - T_C / T_H;

METHODS
METHOD on_load;
	RUN ClearAll;

	FIX BO.eta;
	BO.eta := 1.0;

	FIX TU1.eta;
	TU1.eta := 1.0;
	FIX TU2.eta;
	TU2.eta := 1.0;

	FIX PU1.eta; PU1.eta := 1.0;
	FIX PU2.eta; PU2.eta := 1.0;

	FIX mdot;
	mdot := 900 {t/d};
		
	FIX PU2.outlet.p;
	PU2.outlet.p := 250 {bar};

	FIX BO.inlet.T;
	BO.inlet.T := 200 {K} + 273.15 {K};
	
	FIX TU1.outlet.p;
	TU1.outlet.p := 80 {bar};

	FIX TU2.outlet.p;
	TU2.outlet.p := 1 {bar};

	FIX BO.outlet.h;
	BO.outlet.h := 3772 {kJ/kg};

	FIX CO.outlet.h;
	CO.outlet.h := 104.9 {kJ/kg};

	HE.cons_mass.included := FALSE;
	
END on_load;
END rankine_regen;

MODEL rankine_compare;
	simple IS_A rankine;
	regen IS_A rankine_regen;
	simple.BO.inlet.p, regen.BO.inlet.p ARE_THE_SAME;
	simple.BO.inlet.h, regen.BO.inlet.h ARE_THE_SAME;
	simple.BO.Qdot_fuel, regen.BO.Qdot_fuel ARE_THE_SAME;
	simple.CO.outlet.T, regen.CO.outlet.T ARE_THE_SAME;
	simple.BO.eta, regen.BO.eta ARE_THE_SAME;
	simple.TU.eta, regen.TU1.eta, regen.TU2.eta ARE_THE_SAME;
	simple.PU.eta, regen.PU1.eta, regen.PU2.eta ARE_THE_SAME;
	simple.mdot, regen.mdot ARE_THE_SAME;
METHODS
METHOD on_load;
	RUN ClearAll;
	RUN regen.on_load;
END on_load;
END rankine_compare;




	
