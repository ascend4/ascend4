Import('libascend_env')

csrcs = Split("""

	ascDynaLoad.c ascEnvVar.c ascMalloc.c ascPanic.c
	ascPrint.c error.c mem.c readln.c set.c

""")

configh = libascend_env.SubstInFile(source='config.h.in')

objs = []
for c in csrcs:
	objs += libascend_env.SharedObject(c)

sig_env = libascend_env.Copy()
sig_env.Append(CPPDEFINES=['NO_SIGINT_TRAP','NO_SIGSEGV_TRAP'])
objs += sig_env.SharedObject('ascSignal.c')

#--------------------
# INSTALL

import glob

if libascend_env.get('CAN_INSTALL'):
	installdir = Dir(libascend_env.subst("$INSTALL_ROOT$INSTALL_INCLUDE/utilities"))

	headers = glob.glob("*.h")
	libascend_env.InstallHeader(installdir,headers)

Return('objs')

# vim: set syntax=python:

