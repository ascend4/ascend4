Import('libascend_env')

csrcs = Split("""
	incidence.c
	slv_interface.c
	mps.c
	slv2.c slv6.c slv7.c
	slvDOF.c 
	logblock.c
	solver.c
""")
# slv9, slv3 and slv8 moved to external packages (dynamically loaded)

solver_env = libascend_env.Copy()

defs = []
for i in libascend_env.get('WITH_SOLVERS'):
	solver_env.Append(CPPDEFINES=["STATIC_"+i])

objs = []

if solver_env.get('WITH_CONOPT'):
	solver_env.AppendUnique(CPPPATH=[solver_env.get('CONOPT_CPPPATH')])
	print "CONOPT CPPPATH=%s" % solver_env.get('CPPPATH')

	if solver_env.get('LINKED_CONOPT'):
		libascend_env.AppendUnique(
			LIBPATH=[solver_env.get('CONOPT_LIBPATH')]
			, LIBS=[solver_env.get('CONOPT_LIB')]
		)

	objs += solver_env.SharedObject('conopt.c')
#	print "BUILDING CONOPT.OS"

for c in csrcs:
	objs += solver_env.SharedObject(c)

#--------------------
# INSTALL

import glob

if libascend_env.get('CAN_INSTALL'):
	installdir = Dir(libascend_env.subst("$INSTALL_ROOT$INSTALL_INCLUDE/solver"))

	headers = glob.glob("*.h")
	libascend_env.InstallHeader(installdir,headers)

Return('objs')

# vim: set syntax=python:

