Import('libascend_env')

csrcs = Split("""

	analyze.c bnd.c bndman.c calc.c cond_config.c
	conditional.c conopt.c discrete.c incidence.c linsol.c
	linsolqr.c linutils.c logrel.c logrelman.c model_reorder.c
	mps.c mtx_basic.c mtx_linal.c mtx_perms.c mtx_query.c
	mtx_reorder.c mtx_use_only.c rel.c relman.c
	slv.c slv0.c slv1.c slv2.c slv3.c slv6.c slv7.c
	slv8.c slv9.c slv9a.c slvDOF.c slv_common.c
	slv_interface.c slv_stdcalls.c system.c var.c

	integrator.c lsode.c samplelist.c
""")

solver_env = libascend_env.Copy()

defs = []
for i in libascend_env.get('WITH_SOLVERS'):
	solver_env.Append(CPPDEFINES=["STATIC_"+i])

objs = []
for c in csrcs:
	objs += solver_env.SharedObject(c)

if solver_env.get('WITH_IDA'):
	sundials_env = solver_env.Copy()
	sundials_env.Append(
		CPPPATH=([solver_env.get('IDA_CPPPATH')])
	)
	if solver_env.get('IDA_CPPPATH_EXTRA'):
		sundials_env.AppendUnique(
			CPPPATH=solver_env.get('IDA_CPPPATH_EXTRA')
		)

	libascend_env.AppendUnique(
		LIBPATH=[solver_env.get('IDA_LIBPPATH')]
		, LIBS=[solver_env.get('IDA_LIB')]
	)

	objs += sundials_env.SharedObject('ida.c');
else:
	print "NO IDA REQUESTED!"

if 'LSOD' in solver_env.get('WITH_SOLVERS'):
	if solver_env.get('F2C_LIBPATH'):
		libascend_env.AppendUnique(LIBPATH=[solver_env.get('F2C_LIBPATH')])
	if solver_env.get('F2C_LIB'):
		libascend_env.AppendUnique(LIBS=[solver_env.get('F2C_LIB')])

#--------------------
# INSTALL

import glob

if libascend_env.get('CAN_INSTALL'):
	installdir = libascend_env['INSTALL_ROOT']+libascend_env['INSTALL_INCLUDE']+'/solver'

	headers = glob.glob("*.h")
	libascend_env.InstallHeader(installdir,headers)

Return('objs')
